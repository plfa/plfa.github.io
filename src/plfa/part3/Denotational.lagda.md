---
title     : "Denotational: Denotational semantics of untyped lambda calculus ğŸš§"
layout    : page
prev      : /BigStep/
permalink : /Denotational/
next      : /Compositional/
---

```
module plfa.part3.Denotational where
```

The lambda calculus is a language about _functions_, that is, mappings
from input to output. In computing we often think of such
mappings as being carried out by a sequence of
operations that transform an input into an output.  But
functions can also be represented as data. For example, one
can tabulate a function, that is, create a table where each row has
two entries, an input and the corresponding output for the function.
Function application is then the process of looking up the row for
a given input and reading off the output.

We shall create a semantics for the untyped lambda calculus based on
this idea of functions-as-tables. However, there are two difficulties
that arise. First, functions often have an infinite domain, so it
would seem that we would need infinitely long tables to represent
functions. Second, in the lambda calculus, functions can be applied to
functions. They can even be applied to themselves! So it would seem
that the tables would contain cycles. One might start to worry that
advanced techniques are necessary to to address these issues, but
fortunately this is not the case!

The first problem, of functions with infinite domains, is solved by
observing that in the execution of a terminating program, each lambda
abstraction will only be applied to a finite number of distinct
arguments. (We come back later to discuss diverging programs.) This
observation is another way of looking at Dana Scott's insight that
only continuous functions are needed to model the lambda calculus.

The second problem, that of self-application, is solved by relaxing
the way in which we lookup an argument in a function's table.
Naively, one would look in the table for a row in which the input
entry exactly matches the argument. In the case of self-application,
this would require the table to contain a copy of itself. Impossible!
(At least, it is impossible if we want to build tables using inductive
data type definitions, which indeed we do.)  Instead it is sufficient
to find an input such that every row of the input appears as a row of
the argument (that is, the input is a subset of the argument).  In the
case of self-application, the table only needs to contain a smaller
copy of itself, which is fine.

With these two observations in hand, it is straightforward to write
down a denotational semantics of the lambda calculus.

## Imports

<!-- JGS: for equational reasoning
open import Relation.Binary using (Setoid)
-->
```
open import Agda.Primitive using (lzero; lsuc)
open import Data.Empty using (âŠ¥-elim)
open import Data.Product using (_Ã—_; Î£; Î£-syntax; âˆƒ; âˆƒ-syntax; projâ‚; projâ‚‚)
  renaming (_,_ to âŸ¨_,_âŸ©)
open import Data.Sum
open import Relation.Binary.PropositionalEquality
  using (_â‰¡_; _â‰¢_; refl; sym; cong; congâ‚‚; cong-app)
open import Relation.Nullary using (Â¬_)
open import Relation.Nullary.Negation using (contradiction)
open import Function using (_âˆ˜_)
open import plfa.part2.Untyped
  using (Context; â˜…; _âˆ‹_; âˆ…; _,_; Z; S_; _âŠ¢_; `_; _Â·_; Æ›_;
         #_; twoá¶œ; ext; rename; exts; subst; subst-zero; _[_])
open import plfa.part2.Substitution using (Rename; extensionality; rename-id)
```


## Values

The `Value` data type represents a finite portion of a function.  We
think of a value as a finite set of pairs that represent input-output
mappings. The `Value` data type represents the set as a binary tree
whose internal nodes are the union operator and whose leaves represent
either a single mapping or the empty set.

  * The âŠ¥ value provides no information about the computation.

  * A value of the form `v â†¦ w` is a single input-output mapping, from
    input `v` to output `w`.

  * A value of the form `v âŠ” w` is a function that maps inputs to
    outputs according to both `v` and `w`.  Think of it as taking the
    union of the two sets.

```
infixr 7 _â†¦_
infixl 5 _âŠ”_

data Value : Set where
  âŠ¥ : Value
  _â†¦_ : Value â†’ Value â†’ Value
  _âŠ”_ : Value â†’ Value â†’ Value
```

The `âŠ‘` relation adapts the familiar notion of subset to the Value data
type. This relation plays the key role in enabling self-application.
There are two rules that are specific to functions, `âŠ‘-fun` and `âŠ‘-dist`,
which we discuss below.

```
infix 4 _âŠ‘_

data _âŠ‘_ : Value â†’ Value â†’ Set where

  âŠ‘-bot : âˆ€ {v} â†’ âŠ¥ âŠ‘ v

  âŠ‘-conj-L : âˆ€ {u v w}
    â†’ v âŠ‘ u
    â†’ w âŠ‘ u
      -----------
    â†’ (v âŠ” w) âŠ‘ u

  âŠ‘-conj-R1 : âˆ€ {u v w}
    â†’ u âŠ‘ v
      -----------
    â†’ u âŠ‘ (v âŠ” w)

  âŠ‘-conj-R2 : âˆ€ {u v w}
    â†’ u âŠ‘ w
      -----------
    â†’ u âŠ‘ (v âŠ” w)

  âŠ‘-trans : âˆ€ {u v w}
    â†’ u âŠ‘ v
    â†’ v âŠ‘ w
      -----
    â†’ u âŠ‘ w

  âŠ‘-fun : âˆ€ {v w vâ€² wâ€²}
    â†’ vâ€² âŠ‘ v
    â†’ w âŠ‘ wâ€²
      -------------------
    â†’ (v â†¦ w) âŠ‘ (vâ€² â†¦ wâ€²)

  âŠ‘-dist : âˆ€{v w wâ€²}
      ---------------------------------
    â†’ v â†¦ (w âŠ” wâ€²) âŠ‘ (v â†¦ w) âŠ” (v â†¦ wâ€²)
```


The first five rules are straightforward.
The rule `âŠ‘-fun` captures when it is OK to match a higher-order argument
`vâ€² â†¦ wâ€²` to a table entry whose input is `v â†¦ w`.  Considering a
call to the higher-order argument. It is OK to pass a larger argument
than expected, so `v` can be larger than `vâ€²`. Also, it is OK to
disregard some of the output, so `w` can be smaller than `wâ€²`.
The rule `âŠ‘-dist` says that if you have two entries for the same input,
then you can combine them into a single entry and joins the two
outputs.

The `âŠ‘` relation is reflexive.

```
âŠ‘-refl : âˆ€ {v} â†’ v âŠ‘ v
âŠ‘-refl {âŠ¥} = âŠ‘-bot
âŠ‘-refl {v â†¦ vâ€²} = âŠ‘-fun âŠ‘-refl âŠ‘-refl
âŠ‘-refl {vâ‚ âŠ” vâ‚‚} = âŠ‘-conj-L (âŠ‘-conj-R1 âŠ‘-refl) (âŠ‘-conj-R2 âŠ‘-refl)
```

The `âŠ”` operation is monotonic with respect to `âŠ‘`, that is, given two
larger values it produces a larger value.

```
âŠ”âŠ‘âŠ” : âˆ€ {v w vâ€² wâ€²}
  â†’ v âŠ‘ vâ€²  â†’  w âŠ‘ wâ€²
    -----------------------
  â†’ (v âŠ” w) âŠ‘ (vâ€² âŠ” wâ€²)
âŠ”âŠ‘âŠ” dâ‚ dâ‚‚ = âŠ‘-conj-L (âŠ‘-conj-R1 dâ‚) (âŠ‘-conj-R2 dâ‚‚)
```

The `âŠ‘-dist` rule can be used to combine two entries even when the
input values are not identical. One can first combine the two inputs
using âŠ” and then apply the `âŠ‘-dist` rule to obtain the following
property.

```
âŠ”â†¦âŠ”-dist : âˆ€{v vâ€² w wâ€² : Value}
  â†’ (v âŠ” vâ€²) â†¦ (w âŠ” wâ€²) âŠ‘ (v â†¦ w) âŠ” (vâ€² â†¦ wâ€²)
âŠ”â†¦âŠ”-dist = âŠ‘-trans âŠ‘-dist (âŠ”âŠ‘âŠ” (âŠ‘-fun (âŠ‘-conj-R1 âŠ‘-refl) âŠ‘-refl)
                            (âŠ‘-fun (âŠ‘-conj-R2 âŠ‘-refl) âŠ‘-refl))
```

<!--
 [PLW: above might read more nicely if we introduce inequational reasoning.]
 -->

If the join `u âŠ” v` is less than another value `w`,
then both `u` and `v` are less than `w`.

```
âŠ”âŠ‘-invL : âˆ€{u v w : Value}
  â†’ u âŠ” v âŠ‘ w
    ---------
  â†’ u âŠ‘ w
âŠ”âŠ‘-invL (âŠ‘-conj-L lt1 lt2) = lt1
âŠ”âŠ‘-invL (âŠ‘-conj-R1 lt) = âŠ‘-conj-R1 (âŠ”âŠ‘-invL lt)
âŠ”âŠ‘-invL (âŠ‘-conj-R2 lt) = âŠ‘-conj-R2 (âŠ”âŠ‘-invL lt)
âŠ”âŠ‘-invL (âŠ‘-trans lt1 lt2) = âŠ‘-trans (âŠ”âŠ‘-invL lt1) lt2

âŠ”âŠ‘-invR : âˆ€{u v w : Value}
  â†’ u âŠ” v âŠ‘ w
    ---------
  â†’ v âŠ‘ w
âŠ”âŠ‘-invR (âŠ‘-conj-L lt1 lt2) = lt2
âŠ”âŠ‘-invR (âŠ‘-conj-R1 lt) = âŠ‘-conj-R1 (âŠ”âŠ‘-invR lt)
âŠ”âŠ‘-invR (âŠ‘-conj-R2 lt) = âŠ‘-conj-R2 (âŠ”âŠ‘-invR lt)
âŠ”âŠ‘-invR (âŠ‘-trans lt1 lt2) = âŠ‘-trans (âŠ”âŠ‘-invR lt1) lt2
```


## Environments

An environment gives meaning to the free variables in a term by
mapping variables to values.

```
Env : Context â†’ Set
Env Î“ = âˆ€ (x : Î“ âˆ‹ â˜…) â†’ Value
```

We have the empty environment, and we can extend an environment.
```
`âˆ… : Env âˆ…
`âˆ… ()

infixl 5 _`,_

_`,_ : âˆ€ {Î“} â†’ Env Î“ â†’ Value â†’ Env (Î“ , â˜…)
(Î³ `, v) Z = v
(Î³ `, v) (S x) = Î³ x
```

We can recover the initial environment from an extended environment,
and the last value. Putting them back together again takes us where we started.
```
init : âˆ€ {Î“} â†’ Env (Î“ , â˜…) â†’ Env Î“
init Î³ x = Î³ (S x)

last : âˆ€ {Î“} â†’ Env (Î“ , â˜…) â†’ Value
last Î³ = Î³ Z

init-last : âˆ€ {Î“} â†’ (Î³ : Env (Î“ , â˜…)) â†’ Î³ â‰¡ (init Î³ `, last Î³)
init-last {Î“} Î³ = extensionality lemma
  where lemma : âˆ€ (x : Î“ , â˜… âˆ‹ â˜…) â†’ Î³ x â‰¡ (init Î³ `, last Î³) x
        lemma Z      =  refl
        lemma (S x)  =  refl
```

The nth function takes a de Bruijn index and finds the corresponding
value in the environment.

```
nth : âˆ€{Î“} â†’ (Î“ âˆ‹ â˜…) â†’ Env Î“ â†’ Value
nth x Ï = Ï x
```

We extend the `âŠ‘` relation point-wise to environments with the
following definition.

```
_`âŠ‘_ : âˆ€ {Î“} â†’ Env Î“ â†’ Env Î“ â†’ Set
_`âŠ‘_ {Î“} Î³ Î´ = âˆ€ (x : Î“ âˆ‹ â˜…) â†’ Î³ x âŠ‘ Î´ x
```

We define a bottom environment and a join operator on environments,
which takes the point-wise join of their values.

```
`âŠ¥ : âˆ€ {Î“} â†’ Env Î“
`âŠ¥ x = âŠ¥

_`âŠ”_ : âˆ€ {Î“} â†’ Env Î“ â†’ Env Î“ â†’ Env Î“
(Î³ `âŠ” Î´) x = Î³ x âŠ” Î´ x
```

The `âŠ‘-refl`, `âŠ‘-conj-R1`, and `âŠ‘-conj-R2` rules lift to environments.  So
the join of two environments `Î³` and `Î´` is greater than the first
environment `Î³` or the second environment `Î´`.

```
`âŠ‘-refl : âˆ€ {Î“} {Î³ : Env Î“} â†’ Î³ `âŠ‘ Î³
`âŠ‘-refl {Î“} {Î³} x = âŠ‘-refl {Î³ x}

âŠ‘-env-conj-R1 : âˆ€ {Î“} â†’ (Î³ : Env Î“) â†’ (Î´ : Env Î“) â†’ Î³ `âŠ‘ (Î³ `âŠ” Î´)
âŠ‘-env-conj-R1 Î³ Î´ x = âŠ‘-conj-R1 âŠ‘-refl

âŠ‘-env-conj-R2 : âˆ€ {Î“} â†’ (Î³ : Env Î“) â†’ (Î´ : Env Î“) â†’ Î´ `âŠ‘ (Î³ `âŠ” Î´)
âŠ‘-env-conj-R2 Î³ Î´ x = âŠ‘-conj-R2 âŠ‘-refl
```

## Denotational Semantics

We define the semantics with a judgment of the form `Ï âŠ¢ M â†“ v`,
where `Ï` is the environment, `M` the program, and `v` is a result value.
For readers familiar with big-step semantics, this notation will feel
quite natural, but don't let the similarity fool you.  There are
subtle but important differences! So here is the definition of the
semantics, which we discuss in detail in the following paragraphs.

```
infix 3 _âŠ¢_â†“_

data _âŠ¢_â†“_ : âˆ€{Î“} â†’ Env Î“ â†’ (Î“ âŠ¢ â˜…) â†’ Value â†’ Set where

  var : âˆ€ {Î“} {Î³ : Env Î“} {x}
      ---------------
    â†’ Î³ âŠ¢ (` x) â†“ Î³ x

  â†¦-elim : âˆ€ {Î“} {Î³ : Env Î“} {L M v w}
    â†’ Î³ âŠ¢ L â†“ (v â†¦ w)
    â†’ Î³ âŠ¢ M â†“ v
      ---------------
    â†’ Î³ âŠ¢ (L Â· M) â†“ w

  â†¦-intro : âˆ€ {Î“} {Î³ : Env Î“} {N v w}
    â†’ Î³ `, v âŠ¢ N â†“ w
      -------------------
    â†’ Î³ âŠ¢ (Æ› N) â†“ (v â†¦ w)

  âŠ¥-intro : âˆ€ {Î“} {Î³ : Env Î“} {M}
      ---------
    â†’ Î³ âŠ¢ M â†“ âŠ¥

  âŠ”-intro : âˆ€ {Î“} {Î³ : Env Î“} {M v w}
    â†’ Î³ âŠ¢ M â†“ v
    â†’ Î³ âŠ¢ M â†“ w
      ---------------
    â†’ Î³ âŠ¢ M â†“ (v âŠ” w)

  sub : âˆ€ {Î“} {Î³ : Env Î“} {M v w}
    â†’ Î³ âŠ¢ M â†“ v
    â†’ w âŠ‘ v
      ---------
    â†’ Î³ âŠ¢ M â†“ w
```

Consider the rule for lambda abstractions, `â†¦-intro`.  It says that a
lambda abstraction results in a single-entry table that maps the input
`v` to the output `w`, provided that evaluating the body in an
environment with `v` bound to its parameter produces the output `w`.
As a simple example of this rule, we can see that the identity function
maps `âŠ¥` to `âŠ¥`.

```
id : âˆ… âŠ¢ â˜…
id = Æ› # 0
```

```
denot-id : âˆ€ {Î³ v} â†’ Î³ âŠ¢ id â†“ v â†¦ v
denot-id = â†¦-intro var

denot-id-two : âˆ€ {Î³ v w} â†’ Î³ âŠ¢ id â†“ (v â†¦ v) âŠ” (w â†¦ w)
denot-id-two = âŠ”-intro denot-id denot-id
```

Of course, we will need tables with many rows to capture the meaning
of lambda abstractions. These can be constructed using the `âŠ”-intro`
rule.  If term M (typically a lambda abstraction) can produce both
tables `v` and `w`, then it produces the combined table `v âŠ” w`. One
can take an operational view of the rules `â†¦-intro` and `âŠ”-intro` by
imagining that when an interpreter first comes to a lambda
abstraction, it pre-evaluates the function on a bunch of randomly
chosen arguments, using many instances of the rule `â†¦-intro`, and then
joins them into a big table using many instances of the rule `âŠ”-intro`.
In the following we show that the identity function produces a table
containing both of the previous results, `âŠ¥ â†¦ âŠ¥` and `(âŠ¥ â†¦ âŠ¥) â†¦ (âŠ¥ â†¦
âŠ¥)`.

```
denot-id3 : `âˆ… âŠ¢ id â†“ (âŠ¥ â†¦ âŠ¥) âŠ” (âŠ¥ â†¦ âŠ¥) â†¦ (âŠ¥ â†¦ âŠ¥)
denot-id3 = denot-id-two
```

We most often think of the judgment `Î³ âŠ¢ M â†“ v` as taking the
environment `Î³` and term `M` as input, producing the result `v`.  However,
it is worth emphasizing that the semantics is a _relation_.  The above
results for the identity function show that the same environment and
term can be mapped to different results. However, the results for a
given `Î³` and `M` are not _too_ different, they are all finite
approximations of the same function. Perhaps a better way of thinking
about the judgment `Î³ âŠ¢ M â†“ v` is that the `Î³`, `M`, and `v` are all inputs
and the semantics either confirms or denies whether `v` is an accurate
partial description of the result of `M` in environment `Î³`.

Next we consider the meaning of function application as given by the
`â†¦-elim` rule. In the premise of the rule we have that `L` maps `v` to
`w`. So if `M` produces `v`, then the application of `L` to `M`
produces `w`.

As an example of function application and the `â†¦-elim` rule, we apply
the identity function to itself.  Indeed, we have both that
`âˆ… âŠ¢ id â†“ (u â†¦ u) â†¦ (u â†¦ u)` and also `âˆ… âŠ¢ id â†“ (u â†¦ u)`, so we can
apply the rule `â†¦-elim`.

```
id-app-id : âˆ€ {u : Value} â†’ `âˆ… âŠ¢ id Â· id â†“ (u â†¦ u)
id-app-id {u} = â†¦-elim (â†¦-intro var) (â†¦-intro var)
```

Next we revisit the Church numeral two.  This function has two
parameters: a function and an arbitrary value `u`, and it applies the
function twice. So the function must map `u` to some value, which
we'll name `v`. Then for the second application, it must map `v` to
some value. Let's name it `w`. So the parameter's table must contain
two entries, both `u â†¦ v` and `v â†¦ w`. For each application of the
table, we extract the appropriate entry from it using the `sub` rule.
In particular, we use the âŠ‘-conj-R1 and âŠ‘-conj-R2 to select `u â†¦ v` and `v
â†¦ w`, respectively, from the table `u â†¦ v âŠ” v â†¦ w`. So the meaning of
twoá¶œ is that it takes this table and parameter `u`, and it returns `w`.
Indeed we derive this as follows.

```
denot-twoá¶œ : âˆ€{u v w : Value} â†’ `âˆ… âŠ¢ twoá¶œ â†“ ((u â†¦ v âŠ” v â†¦ w) â†¦ (u â†¦ w))
denot-twoá¶œ {u}{v}{w} =
  â†¦-intro (â†¦-intro (â†¦-elim (sub var lt1) (â†¦-elim (sub var lt2) var)))
  where lt1 : v â†¦ w âŠ‘ u â†¦ v âŠ” v â†¦ w
        lt1 = âŠ‘-conj-R2 (âŠ‘-fun âŠ‘-refl âŠ‘-refl)

        lt2 : u â†¦ v âŠ‘ u â†¦ v âŠ” v â†¦ w
        lt2 = (âŠ‘-conj-R1 (âŠ‘-fun âŠ‘-refl âŠ‘-refl))
```


Next we have a classic example of self application: `Î” = Î»x. (x x)`.
The input value for `x` needs to be a table, and it needs to have an
entry that maps a smaller version of itself, call it `v`, to some value
`w`. So the input value looks like `v â†¦ w âŠ” v`. Of course, then the
output of `Î”` is `w`. The derivation is given below.  The first occurrences
of `x` evaluates to `v â†¦ w`, the second occurrence of `x` evaluates to `v`,
and then the result of the application is `w`.

```
Î” : âˆ… âŠ¢ â˜…
Î” = (Æ› (# 0) Â· (# 0))

denot-Î” : âˆ€ {v w} â†’ `âˆ… âŠ¢ Î” â†“ ((v â†¦ w âŠ” v) â†¦ w)
denot-Î” = â†¦-intro (â†¦-elim (sub var (âŠ‘-conj-R1 âŠ‘-refl))
                          (sub var (âŠ‘-conj-R2 âŠ‘-refl)))
```

One might worry whether this semantics can deal with diverging
programs.  The `âŠ¥` value and the `âŠ¥-intro` rule provide a way to handle
them. (The `âŠ¥-intro` rule is also what enables Î² reduction on
non-terminating arguments.)  The classic `Î©` program is a particularly
simple program that diverges. It applies `Î”` to itself. The semantics
assigns to `Î©` the meaning `âŠ¥`. There are several ways to derive this, we
shall start with one that makes use of the `âŠ”-intro` rule.  First,
`denot-Î”` tells us that `Î”` evaluates to `((âŠ¥ â†¦ âŠ¥) âŠ” âŠ¥) â†¦ âŠ¥`
(choose `vâ‚ = vâ‚‚ = âŠ¥`).  Next, `Î”` also evaluates to `âŠ¥ â†¦ âŠ¥` by use of
`â†¦-intro` and `âŠ¥-intro` and to `âŠ¥` by `âŠ¥-intro`. As we saw previously,
whenever we can show that a program evaluates to two values, we can apply
`âŠ”-intro` to join them together, so `Î”` evaluates to `(âŠ¥ â†¦ âŠ¥) âŠ” âŠ¥`. This
matches the input of the first occurrence of `Î”`, so we can conclude that
the result of the application is `âŠ¥`.

```
Î© : âˆ… âŠ¢ â˜…
Î© = Î” Â· Î”

denot-Î© : `âˆ… âŠ¢ Î© â†“ âŠ¥
denot-Î© = â†¦-elim denot-Î” (âŠ”-intro (â†¦-intro âŠ¥-intro) âŠ¥-intro)
```

A shorter derivation of the same result is by just one use of the
`âŠ¥-intro` rule.

```
denot-Î©' : `âˆ… âŠ¢ Î© â†“ âŠ¥
denot-Î©' = âŠ¥-intro
```

Just because one can derive `âˆ… âŠ¢ M â†“ âŠ¥` for some closed term `M` doesn't mean
that `M` necessarily diverges. There may be other derivations that
conclude with `M` producing some more informative value.  However, if
the only thing that a term evaluates to is `âŠ¥`, then it indeed diverges.

An attentive reader may have noticed a disconnect earlier in the way
we planned to solve the self-application problem and the actual
`â†¦-elim` rule for application. We said at the beginning that we would
relax the notion of table lookup, allowing an argument to match an
input entry if the argument is equal or greater than the input entry.
Instead, the `â†¦-elim` rule seems to require an exact match.  However,
because of the `sub` rule, application really does allow larger
arguments.

```
â†¦-elim2 : âˆ€ {Î“} {Î³ : Env Î“} {Mâ‚ Mâ‚‚ vâ‚ vâ‚‚ vâ‚ƒ}
  â†’ Î³ âŠ¢ Mâ‚ â†“ (vâ‚ â†¦ vâ‚ƒ)
  â†’ Î³ âŠ¢ Mâ‚‚ â†“ vâ‚‚
  â†’ vâ‚ âŠ‘ vâ‚‚
    ------------------
  â†’ Î³ âŠ¢ (Mâ‚ Â· Mâ‚‚) â†“ vâ‚ƒ
â†¦-elim2 dâ‚ dâ‚‚ lt = â†¦-elim dâ‚ (sub dâ‚‚ lt)
```

#### Exercise `denot-plusá¶œ` (practice)

What is a denotation for `plusá¶œ`? That is, find a value `v` (other than `âŠ¥`)
such that `âˆ… âŠ¢ plusá¶œ â†“ v`. Also, give the proof of `âˆ… âŠ¢ plusá¶œ â†“ v`
for your choice of `v`.

```
-- Your code goes here
```

## Denotations and denotational equality

Next we define a notion of denotational equality based on the above
semantics. Its statement makes use of an if-and-only-if, which we
define as follows.

```
_iff_ : Set â†’ Set â†’ Set
P iff Q = (P â†’ Q) Ã— (Q â†’ P)
```

Another way to view the denotational semantics is as a function that
maps a term to a relation from environments to values.  That is, the
_denotation_ of a term is a relation from environments to values.

```
Denotation : Context â†’ Setâ‚
Denotation Î“ = (Env Î“ â†’ Value â†’ Set)
```

The following function â„° gives this alternative view of the semantics,
which really just amounts to changing the order of the parameters.

```
â„° : âˆ€{Î“} â†’ (M : Î“ âŠ¢ â˜…) â†’ Denotation Î“
â„° M = Î» Î³ v â†’ Î³ âŠ¢ M â†“ v
```

In general, two denotations are equal when they produce the same
values in the same environment.

```
infix 3 _â‰ƒ_

_â‰ƒ_ : âˆ€ {Î“} â†’ (Denotation Î“) â†’ (Denotation Î“) â†’ Set
(_â‰ƒ_ {Î“} Dâ‚ Dâ‚‚) = (Î³ : Env Î“) â†’ (v : Value) â†’ Dâ‚ Î³ v iff Dâ‚‚ Î³ v
```

Denotational equality is an equivalence relation.

```
â‰ƒ-refl : âˆ€ {Î“ : Context} â†’ {M : Denotation Î“}
  â†’ M â‰ƒ M
â‰ƒ-refl Î³ v = âŸ¨ (Î» x â†’ x) , (Î» x â†’ x) âŸ©

â‰ƒ-sym : âˆ€ {Î“ : Context} â†’ {M N : Denotation Î“}
  â†’ M â‰ƒ N
    -----
  â†’ N â‰ƒ M
â‰ƒ-sym eq Î³ v = âŸ¨ (projâ‚‚ (eq Î³ v)) , (projâ‚ (eq Î³ v)) âŸ©

â‰ƒ-trans : âˆ€ {Î“ : Context} â†’ {Mâ‚ Mâ‚‚ Mâ‚ƒ : Denotation Î“}
  â†’ Mâ‚ â‰ƒ Mâ‚‚
  â†’ Mâ‚‚ â‰ƒ Mâ‚ƒ
    -------
  â†’ Mâ‚ â‰ƒ Mâ‚ƒ
â‰ƒ-trans eq1 eq2 Î³ v = âŸ¨ (Î» z â†’ projâ‚ (eq2 Î³ v) (projâ‚ (eq1 Î³ v) z)) ,
                        (Î» z â†’ projâ‚‚ (eq1 Î³ v) (projâ‚‚ (eq2 Î³ v) z)) âŸ©
```

Two terms `M` and `N` are denotational equal when their denotations are
equal, that is, `â„° M â‰ƒ â„° N`.

The following submodule introduces equational reasoning for the `â‰ƒ`
relation.

<!--

JGS: I couldn't get this to work. The definitions here were accepted
by Agda, but then the uses in the Compositional chapter got rejected.

denotation-setoid : Context â†’ Setoid (lsuc lzero) lzero
denotation-setoid Î“ = record
  { Carrier = Denotation Î“
  ; _â‰ˆ_ = _â‰ƒ_
  ; isEquivalence = record { refl = â‰ƒ-refl ; sym = â‰ƒ-sym ; trans = â‰ƒ-trans } }
-->
<!--
  The following went inside the module â‰ƒ-Reasoning:
  
  open import Relation.Binary.Reasoning.Setoid (denotation-setoid Î“)
    renaming (begin_ to start_; _â‰ˆâŸ¨_âŸ©_ to _â‰ƒâŸ¨_âŸ©_; _âˆ to _â˜) public
-->
```

module â‰ƒ-Reasoning {Î“ : Context} where

  infix  1 start_
  infixr 2 _â‰ƒâŸ¨âŸ©_ _â‰ƒâŸ¨_âŸ©_
  infix  3 _â˜

  start_ : âˆ€ {x y : Denotation Î“}
    â†’ x â‰ƒ y
      -----
    â†’ x â‰ƒ y
  start xâ‰ƒy  =  xâ‰ƒy

  _â‰ƒâŸ¨_âŸ©_ : âˆ€ (x : Denotation Î“) {y z : Denotation Î“}
    â†’ x â‰ƒ y
    â†’ y â‰ƒ z
      -----
    â†’ x â‰ƒ z
  (x â‰ƒâŸ¨ xâ‰ƒy âŸ© yâ‰ƒz) =  â‰ƒ-trans xâ‰ƒy yâ‰ƒz

  _â‰ƒâŸ¨âŸ©_ : âˆ€ (x : Denotation Î“) {y : Denotation Î“}
    â†’ x â‰ƒ y
      -----
    â†’ x â‰ƒ y
  x â‰ƒâŸ¨âŸ© xâ‰ƒy  =  xâ‰ƒy

  _â˜ : âˆ€ (x : Denotation Î“)
      -----
    â†’ x â‰ƒ x
  (x â˜)  =  â‰ƒ-refl
```

## Road map for the following chapters

The subsequent chapters prove that the denotational semantics has
several desirable properties. First, we prove that the semantics is
compositional, i.e., that the denotation of a term is a function of
the denotations of its subterms. To do this we shall prove equations
of the following shape.

    â„° (` x) â‰ƒ ...
    â„° (Æ› M) â‰ƒ ... â„° M ...
    â„° (M Â· N) â‰ƒ ... â„° M ... â„° N ...

The compositionality property is not trivial because the semantics we
have defined includes three rules that are not syntax directed:
`âŠ¥-intro`, `âŠ”-intro`, and `sub`. The above equations suggest that the
dentoational semantics can be defined as a recursive function, and
indeed, we give such a definition and prove that it is equivalent to
â„°.

Next we investigate whether the denotational semantics and the
reduction semantics are equivalent. Recall that the job of a language
semantics is to describe the observable behavior of a given program
`M`. For the lambda calculus there are several choices that one can
make, but they usually boil down to a single bit of information:

  * divergence: the program `M` executes forever.
  * termination: the program `M` halts.

We can characterize divergence and termination in terms of reduction.

  * divergence: `Â¬ (M â€”â†  Æ› N)` for any term `N`.
  * termination: `M â€”â†  Æ› N` for some term `N`.

We can also characterize divergence and termination using denotations.

  * divergence: `Â¬ (âˆ… âŠ¢ M â†“ v â†¦ w)` for any `v` and `w`.
  * termination: `âˆ… âŠ¢ M â†“ v â†¦ w` for some `v` and `w`.

Alternatively, we can use the denotation function `â„°`.

  * divergence: `Â¬ (â„° M â‰ƒ â„° (Æ› N))` for any term `N`.
  * termination: `â„° M â‰ƒ â„° (Æ› N)` for some term `N`.

So the question is whether the reduction semantics and denotational
semantics are equivalent.

    (âˆƒ N. M â€”â†  Æ› N)  iff  (âˆƒ N. â„° M â‰ƒ â„° (Æ› N))

We address each direction of the equivalence in the second and third
chapters. In the second chapter we prove that reduction to a lambda
abstraction implies denotational equality to a lambda
abstraction. This property is called the _soundness_ in the
literature.

    M â€”â†  Æ› N  implies  â„° M â‰ƒ â„° (Æ› N)

In the third chapter we prove that denotational equality to a lambda
abstraction implies reduction to a lambda abstraction. This property
is called _adequacy_ in the literature.

    â„° M â‰ƒ â„° (Æ› N)  implies M â€”â†  Æ› Nâ€² for some Nâ€²

The fourth chapter applies the results of the three preceeding
chapters (compositionality, soundness, and adequacy) to prove that
denotational equality implies a property called _contextual
equivalence_. This property is important because it justifies the use
of denotational equality in proving the correctness of program
transformations such as performance optimizations.

The proofs of all of these properties rely on some basic results about
the denotational semantics, which we establish in the rest of this
chapter.  We start with some lemmas about renaming, which are quite
similar to the renaming lemmas that we have seen in previous chapters.
We conclude with a proof of an important inversion lemma for the
less-than relation regarding function values.


## Renaming preserves denotations

We shall prove that renaming variables, and changing the environment
accordingly, preserves the meaning of a term. We generalize the
renaming lemma to allow the values in the new environment to be the
same or larger than the original values. This generalization is useful
in proving that reduction implies denotational equality.

As before, we need an extension lemma to handle the case where we
proceed underneath a lambda abstraction. Here, the nth function
performs lookup in the environment, analogous to `Î“ âˆ‹ A`.  Now suppose
that `Ï` is a renaming that maps variables in `Î³` into variables with
equal or larger values in `Î´`. This lemmas says that extending the
renaming producing a renaming `ext r` that maps `Î³ , v` to `Î´ , v`.

```
ext-nth : âˆ€ {Î“ Î” v} {Î³ : Env Î“} {Î´ : Env Î”}
  â†’ (Ï : Rename Î“ Î”)
  â†’ Î³ `âŠ‘ (Î´ âˆ˜ Ï)
    ------------------------------
  â†’ (Î³ `, v) `âŠ‘ ((Î´ `, v) âˆ˜ ext Ï)
ext-nth Ï lt Z = âŠ‘-refl
ext-nth Ï lt (S nâ€²) = lt nâ€²
```

We proceed by cases on the de Bruijn index `n`.

* If it is `Z`, then we just need to show that `v â‰¡ v`, which we have by `refl`.

* If it is `S nâ€²`, then the goal simplifies to `nth nâ€² Î³ â‰¡ nth (Ï nâ€²) Î´`,
  which is an instance of the premise.

Now for the renaming lemma. Suppose we have a renaming that maps
variables in `Î³` into variables with the same values in `Î´`.  If `M`
results in `v` when evaluated in environment `Î³`, then applying the
renaming to `M` produces a program that results in the same value `v` when
evaluated in `Î´`.

```
rename-pres : âˆ€ {Î“ Î” v} {Î³ : Env Î“} {Î´ : Env Î”} {M : Î“ âŠ¢ â˜…}
  â†’ (Ï : Rename Î“ Î”)
  â†’ Î³ `âŠ‘ (Î´ âˆ˜ Ï)
  â†’ Î³ âŠ¢ M â†“ v
    ---------------------
  â†’ Î´ âŠ¢ (rename Ï M) â†“ v
rename-pres Ï lt (var {x = x}) = sub var (lt x)
rename-pres Ï lt (â†¦-elim d dâ‚) =
   â†¦-elim (rename-pres Ï lt d) (rename-pres Ï lt dâ‚)
rename-pres Ï lt (â†¦-intro d) =
   â†¦-intro (rename-pres (ext Ï) (ext-nth Ï lt) d)
rename-pres Ï lt âŠ¥-intro = âŠ¥-intro
rename-pres Ï lt (âŠ”-intro d dâ‚) =
   âŠ”-intro (rename-pres Ï lt d) (rename-pres Ï lt dâ‚)
rename-pres Ï lt (sub d ltâ€²) =
   sub (rename-pres Ï lt d) ltâ€²
```

The proof is by induction on the semantics of `M`.  As you can see, all
of the cases are trivial except the cases for variables and lambda.

* For a variable `x`, we make use of the premise to
  show that `nth x Î³ â‰¡ nth (Ï x) Î´`.

* For a lambda abstraction, the induction hypothesis requires us to
  extend the renaming. We do so, and use the `ext-nth` lemma to show
  that the extended renaming maps variables to ones with equivalent
  values.


## Identity renamings and environment strengthening

We shall need a corollary of the renaming lemma that says that if `M`
results in `v`, then we can replace a value in the environment with a
larger one (a stronger one), and `M` still results in `v`. In
particular, if `Î³ âŠ¢ M â†“ v` and `Î³ âŠ‘ Î´`, then `Î´ âŠ¢ M â†“ v`.  What does
this have to do with renaming?  It's renaming with the identity
function.  So we apply the renaming lemma with the identity renaming,
which gives us `Î´ âŠ¢ rename (Î» {A} x â†’ x) M â†“ v`, and then we apply the
`rename-id` lemma to obtain `Î´ âŠ¢ M â†“ v`.

```
âŠ‘-env : âˆ€ {Î“} {Î³ : Env Î“} {Î´ : Env Î“} {M v}
  â†’ Î³ âŠ¢ M â†“ v
  â†’ Î³ `âŠ‘ Î´
    ----------
  â†’ Î´ âŠ¢ M â†“ v
âŠ‘-env{Î“}{Î³}{Î´}{M}{v} d lt
      with rename-pres{Î“}{Î“}{v}{Î³}{Î´}{M} (Î» {A} x â†’ x) lt d
... | dâ€² rewrite rename-id {Î“}{â˜…}{M} =
      dâ€²
```

In the proof that substitution reflects denotations, in the case for
lambda abstraction, we use a minor variation of `âŠ‘-env`, in which just
the last element of the environment gets larger.

```
up-env : âˆ€ {Î“} {Î³ : Env Î“} {M v uâ‚ uâ‚‚}
  â†’ (Î³ `, uâ‚) âŠ¢ M â†“ v
  â†’ uâ‚ âŠ‘ uâ‚‚
    -----------------
  â†’ (Î³ `, uâ‚‚) âŠ¢ M â†“ v
up-env d lt = âŠ‘-env d (nth-le lt)
  where
  nth-le : âˆ€ {Î³ uâ‚ uâ‚‚} â†’ uâ‚ âŠ‘ uâ‚‚ â†’ (Î³ `, uâ‚) `âŠ‘ (Î³ `, uâ‚‚)
  nth-le lt Z = lt
  nth-le lt (S n) = âŠ‘-refl
```


## Inversion of the less-than relation for functions

What can we deduce from knowing that a function `v â†¦ w` is less than
some value `u`?  What can we deduce about `u`? The answer to this
question is called the inversion property of less-than for functions.
This question is not easy to answer because of the `âŠ‘-dist` rule, which
relates a function on the left to a pair of functions on the right.
So `u` may include several functions that, as a group, relate to
`v â†¦ w`. Furthermore, because of the rules `âŠ‘-conj-R1` and `âŠ‘-conj-R2`,
there may be other values inside `u`, such as `âŠ¥`, that have nothing
to do with `v â†¦ w`. But in general, we can deduce that `u` includes
a collection of functions where the join of their domains is less
than `v` and the join of their codomains is greater than `w`.

To precisely state and prove this inversion property, we need to
define what it means for a value to _include_ a collection of values.
We also need to define how to compute the join of their domains and
codomains.


### Value membership and inclusion

Recall that we think of a value as a set of entries with the join
operator `v âŠ” w` acting like set union. The function value `v â†¦ w` and
bottom value `âŠ¥` constitute the two kinds of elements of the set.  (In
other contexts one can instead think of `âŠ¥` as the empty set, but here
we must think of it as an element.)  We write `u âˆˆ v` to say that `u` is
an element of `v`, as defined below.

```
infix 5 _âˆˆ_

_âˆˆ_ : Value â†’ Value â†’ Set
u âˆˆ âŠ¥ = u â‰¡ âŠ¥
u âˆˆ v â†¦ w = u â‰¡ v â†¦ w
u âˆˆ (v âŠ” w) = u âˆˆ v âŠ u âˆˆ w
```

So we can represent a collection of values simply as a value.  We
write `v âŠ† w` to say that all the elements of `v` are also in `w`.

```
infix 5 _âŠ†_

_âŠ†_ : Value â†’ Value â†’ Set
v âŠ† w = âˆ€{u} â†’ u âˆˆ v â†’ u âˆˆ w
```

The notions of membership and inclusion for values are closely related
to the less-than relation. They are narrower relations in that they
imply the less-than relation but not the other way around.

```
âˆˆâ†’âŠ‘ : âˆ€{u v : Value}
    â†’ u âˆˆ v
      -----
    â†’ u âŠ‘ v
âˆˆâ†’âŠ‘ {.âŠ¥} {âŠ¥} refl = âŠ‘-bot
âˆˆâ†’âŠ‘ {v â†¦ w} {v â†¦ w} refl = âŠ‘-refl
âˆˆâ†’âŠ‘ {u} {v âŠ” w} (injâ‚ x) = âŠ‘-conj-R1 (âˆˆâ†’âŠ‘ x)
âˆˆâ†’âŠ‘ {u} {v âŠ” w} (injâ‚‚ y) = âŠ‘-conj-R2 (âˆˆâ†’âŠ‘ y)

âŠ†â†’âŠ‘ : âˆ€{u v : Value}
    â†’ u âŠ† v
      -----
    â†’ u âŠ‘ v
âŠ†â†’âŠ‘ {âŠ¥} s with s {âŠ¥} refl
... | x = âŠ‘-bot
âŠ†â†’âŠ‘ {u â†¦ uâ€²} s with s {u â†¦ uâ€²} refl
... | x = âˆˆâ†’âŠ‘ x
âŠ†â†’âŠ‘ {u âŠ” uâ€²} s = âŠ‘-conj-L (âŠ†â†’âŠ‘ (Î» z â†’ s (injâ‚ z))) (âŠ†â†’âŠ‘ (Î» z â†’ s (injâ‚‚ z)))
```

We shall also need some inversion principles for value inclusion.  If
the union of `u` and `v` is included in `w`, then of course both `u` and
`v` are each included in `w`.

```
âŠ”âŠ†-inv : âˆ€{u v w : Value}
       â†’ (u âŠ” v) âŠ† w
         ---------------
       â†’ u âŠ† w  Ã—  v âŠ† w
âŠ”âŠ†-inv uvw = âŸ¨ (Î» x â†’ uvw (injâ‚ x)) , (Î» x â†’ uvw (injâ‚‚ x)) âŸ©
```

In our value representation, the function value `v â†¦ w` is both an
element and also a singleton set. So if `v â†¦ w` is a subset of `u`,
then `v â†¦ w` must be a member of `u`.

```
â†¦âŠ†â†’âˆˆ : âˆ€{v w u : Value}
     â†’ v â†¦ w âŠ† u
       ---------
     â†’ v â†¦ w âˆˆ u
â†¦âŠ†â†’âˆˆ incl = incl refl
```


### Function values

To identify collections of functions, we define the following two
predicates. We write `Fun u` if `u` is a function value, that is, if
`u â‰¡ v â†¦ w` for some values `v` and `w`. We write `all-funs v` if all the elements
of `v` are functions.

```
data Fun : Value â†’ Set where
  fun : âˆ€{u v w} â†’ u â‰¡ (v â†¦ w) â†’ Fun u

all-funs : Value â†’ Set
all-funs v = âˆ€{u} â†’ u âˆˆ v â†’ Fun u
```

The value `âŠ¥` is not a function.

```
Â¬FunâŠ¥ : Â¬ (Fun âŠ¥)
Â¬FunâŠ¥ (fun ())
```

In our values-as-sets representation, our sets always include at least
one element. Thus, if all the elements are functions, there is at
least one that is a function.

```
all-funsâˆˆ : âˆ€{u}
      â†’ all-funs u
      â†’ Î£[ v âˆˆ Value ] Î£[ w âˆˆ Value ] v â†¦ w âˆˆ u
all-funsâˆˆ {âŠ¥} f with f {âŠ¥} refl
... | fun ()
all-funsâˆˆ {v â†¦ w} f = âŸ¨ v , âŸ¨ w , refl âŸ© âŸ©
all-funsâˆˆ {u âŠ” uâ€²} f
    with all-funsâˆˆ Î» z â†’ f (injâ‚ z)
... | âŸ¨ v , âŸ¨ w , m âŸ© âŸ© = âŸ¨ v , âŸ¨ w , (injâ‚ m) âŸ© âŸ©
```


### Domains and codomains

Returning to our goal, the inversion principle for less-than a
function, we want to show that `v â†¦ w âŠ‘ u` implies that `u` includes
a set of function values such that the join of their domains is less
than `v` and the join of their codomains is greater than `w`.

To this end we define the following dom and cod functions.  Given some
value `u` (that represents a set of entries), `dom u` returns the join of
their domains and `cod u` returns the join of their codomains.

```
dom : (u : Value) â†’ Value
dom âŠ¥  = âŠ¥
dom (v â†¦ w) = v
dom (u âŠ” uâ€²) = dom u âŠ” dom uâ€²

cod : (u : Value) â†’ Value
cod âŠ¥  = âŠ¥
cod (v â†¦ w) = w
cod (u âŠ” uâ€²) = cod u âŠ” cod uâ€²
```

We need just one property each for `dom` and `cod`.  Given a collection of
functions represented by value `u`, and an entry `v â†¦ w âˆˆ u`, we know
that `v` is included in the domain of `v`.

```
â†¦âˆˆâ†’âŠ†dom : âˆ€{u v w : Value}
          â†’ all-funs u  â†’  (v â†¦ w) âˆˆ u
            ----------------------
          â†’ v âŠ† dom u
â†¦âˆˆâ†’âŠ†dom {âŠ¥} fg () uâˆˆv
â†¦âˆˆâ†’âŠ†dom {v â†¦ w} fg refl uâˆˆv = uâˆˆv
â†¦âˆˆâ†’âŠ†dom {u âŠ” uâ€²} fg (injâ‚ vâ†¦wâˆˆu) uâˆˆv =
   let ih = â†¦âˆˆâ†’âŠ†dom (Î» z â†’ fg (injâ‚ z)) vâ†¦wâˆˆu in
   injâ‚ (ih uâˆˆv)
â†¦âˆˆâ†’âŠ†dom {u âŠ” uâ€²} fg (injâ‚‚ vâ†¦wâˆˆuâ€²) uâˆˆv =
   let ih = â†¦âˆˆâ†’âŠ†dom (Î» z â†’ fg (injâ‚‚ z)) vâ†¦wâˆˆuâ€² in
   injâ‚‚ (ih uâˆˆv)
```

Regarding `cod`, suppose we have a collection of functions represented
by `u`, but all of them are just copies of `v â†¦ w`.  Then the `cod u` is
included in `w`.

```
âŠ†â†¦â†’codâŠ† : âˆ€{u v w : Value}
        â†’ u âŠ† v â†¦ w
          ---------
        â†’ cod u âŠ† w
âŠ†â†¦â†’codâŠ† {âŠ¥} s refl with s {âŠ¥} refl
... | ()
âŠ†â†¦â†’codâŠ† {C â†¦ Câ€²} s m with s {C â†¦ Câ€²} refl
... | refl = m
âŠ†â†¦â†’codâŠ† {u âŠ” uâ€²} s (injâ‚ x) = âŠ†â†¦â†’codâŠ† (Î» {C} z â†’ s (injâ‚ z)) x
âŠ†â†¦â†’codâŠ† {u âŠ” uâ€²} s (injâ‚‚ y) = âŠ†â†¦â†’codâŠ† (Î» {C} z â†’ s (injâ‚‚ z)) y
```

With the `dom` and `cod` functions in hand, we can make precise the
conclusion of the inversion principle for functions, which we package
into the following predicate named `factor`. We say that `v â†¦ w`
_factors_ `u` into `uâ€²` if `uâ€²` is a included in `u`, if `uâ€²` contains only
functions, its domain is less than `v`, and its codomain is greater
than `w`.

```
factor : (u : Value) â†’ (uâ€² : Value) â†’ (v : Value) â†’ (w : Value) â†’ Set
factor u uâ€² v w = all-funs uâ€² Ã— uâ€² âŠ† u Ã— dom uâ€² âŠ‘ v Ã— w âŠ‘ cod uâ€²
```

We prove the inversion principle for functions by induction on the
derivation of the less-than relation. To make the induction hypothesis
stronger, we broaden the premise to `uâ‚ âŠ‘ uâ‚‚` (instead of `v â†¦ w âŠ‘
u`), and strengthen the conclusion to say that for _every_ function
value `v â†¦ w âˆˆ uâ‚`, we have that `v â†¦ w` factors `uâ‚‚` into some
value `uâ‚ƒ`.

### Inversion of less-than for functions, the case for âŠ‘-trans

The crux of the proof is the case for `âŠ‘-trans`.

    uâ‚ âŠ‘ u   u âŠ‘ uâ‚‚
    --------------- (âŠ‘-trans)
        uâ‚ âŠ‘ uâ‚‚

By the induction hypothesis for `uâ‚ âŠ‘ u`, we know
that `v â†¦ w factors u into uâ€²`, for some value `uâ€²`,
so we have `all-funs uâ€²` and `uâ€² âŠ† u`.
By the induction hypothesis for `u âŠ‘ uâ‚‚`, we know
that for any `vâ€² â†¦ wâ€² âˆˆ u`, `vâ€² â†¦ wâ€²` factors `uâ‚‚` into `uâ‚ƒ`.
With these facts in hand, we proceed by induction on `uâ€²`
to prove that `(dom uâ€²) â†¦ (cod uâ€²)` factors `uâ‚‚` into `uâ‚ƒ`.
We discuss each case of the proof in the text below.

```
sub-inv-trans : âˆ€{uâ€² uâ‚‚ u : Value}
    â†’ all-funs uâ€²  â†’  uâ€² âŠ† u
    â†’ (âˆ€{vâ€² wâ€²} â†’ vâ€² â†¦ wâ€² âˆˆ u â†’ Î£[ uâ‚ƒ âˆˆ Value ] factor uâ‚‚ uâ‚ƒ vâ€² wâ€²)
      ---------------------------------------------------------------
    â†’ Î£[ uâ‚ƒ âˆˆ Value ] factor uâ‚‚ uâ‚ƒ (dom uâ€²) (cod uâ€²)
sub-inv-trans {âŠ¥} {uâ‚‚} {u} fuâ€² uâ€²âŠ†u IH =
   âŠ¥-elim (contradiction (fuâ€² refl) Â¬FunâŠ¥)
sub-inv-trans {uâ‚â€² â†¦ uâ‚‚â€²} {uâ‚‚} {u} fg uâ€²âŠ†u IH = IH (â†¦âŠ†â†’âˆˆ uâ€²âŠ†u)
sub-inv-trans {uâ‚â€² âŠ” uâ‚‚â€²} {uâ‚‚} {u} fg uâ€²âŠ†u IH
    with âŠ”âŠ†-inv uâ€²âŠ†u
... | âŸ¨ uâ‚â€²âŠ†u , uâ‚‚â€²âŠ†u âŸ©
    with sub-inv-trans {uâ‚â€²} {uâ‚‚} {u} (Î» {vâ€²} z â†’ fg (injâ‚ z)) uâ‚â€²âŠ†u IH
       | sub-inv-trans {uâ‚‚â€²} {uâ‚‚} {u} (Î» {vâ€²} z â†’ fg (injâ‚‚ z)) uâ‚‚â€²âŠ†u IH
... | âŸ¨ uâ‚ƒâ‚ , âŸ¨ fu21' , âŸ¨ uâ‚ƒâ‚âŠ†uâ‚‚ , âŸ¨ duâ‚ƒâ‚âŠ‘duâ‚â€² , cuâ‚â€²âŠ‘cuâ‚ƒâ‚ âŸ© âŸ© âŸ© âŸ©
    | âŸ¨ uâ‚ƒâ‚‚ , âŸ¨ fu22' , âŸ¨ uâ‚ƒâ‚‚âŠ†uâ‚‚ , âŸ¨ duâ‚ƒâ‚‚âŠ‘duâ‚‚â€² , cuâ‚â€²âŠ‘cuâ‚ƒâ‚‚ âŸ© âŸ© âŸ© âŸ© =
      âŸ¨ (uâ‚ƒâ‚ âŠ” uâ‚ƒâ‚‚) , âŸ¨ fuâ‚‚â€² , âŸ¨ uâ‚‚â€²âŠ†uâ‚‚ ,
      âŸ¨ âŠ”âŠ‘âŠ” duâ‚ƒâ‚âŠ‘duâ‚â€² duâ‚ƒâ‚‚âŠ‘duâ‚‚â€² ,
        âŠ”âŠ‘âŠ” cuâ‚â€²âŠ‘cuâ‚ƒâ‚ cuâ‚â€²âŠ‘cuâ‚ƒâ‚‚ âŸ© âŸ© âŸ© âŸ©
    where fuâ‚‚â€² : {vâ€² : Value} â†’ vâ€² âˆˆ uâ‚ƒâ‚ âŠ vâ€² âˆˆ uâ‚ƒâ‚‚ â†’ Fun vâ€²
          fuâ‚‚â€² {vâ€²} (injâ‚ x) = fu21' x
          fuâ‚‚â€² {vâ€²} (injâ‚‚ y) = fu22' y
          uâ‚‚â€²âŠ†uâ‚‚ : {C : Value} â†’ C âˆˆ uâ‚ƒâ‚ âŠ C âˆˆ uâ‚ƒâ‚‚ â†’ C âˆˆ uâ‚‚
          uâ‚‚â€²âŠ†uâ‚‚ {C} (injâ‚ x) = uâ‚ƒâ‚âŠ†uâ‚‚ x
          uâ‚‚â€²âŠ†uâ‚‚ {C} (injâ‚‚ y) = uâ‚ƒâ‚‚âŠ†uâ‚‚ y
```

* Suppose `uâ€² â‰¡ âŠ¥`. Then we have a contradiction because
  it is not the case that `Fun âŠ¥`.

* Suppose `uâ€² â‰¡ uâ‚â€² â†¦ uâ‚‚â€²`. Then `uâ‚â€² â†¦ uâ‚‚â€² âˆˆ u` and we can apply the
  premise (the induction hypothesis from `u âŠ‘ uâ‚‚`) to obtain that
  `uâ‚â€² â†¦ uâ‚‚â€²` factors of `uâ‚‚ into uâ‚‚â€²`. This case is complete because
  `dom uâ€² â‰¡ uâ‚â€²` and `cod uâ€² â‰¡ uâ‚‚â€²`.

* Suppose `uâ€² â‰¡ uâ‚â€² âŠ” uâ‚‚â€²`. Then we have `uâ‚â€² âŠ† u` and `uâ‚‚â€² âŠ† u`. We also
  have `all-funs uâ‚â€²` and `all-funs uâ‚‚â€²`, so we can apply the induction hypothesis
  for both `uâ‚â€²` and `uâ‚‚â€²`. So there exists values `uâ‚ƒâ‚` and `uâ‚ƒâ‚‚` such that
  `(dom uâ‚â€²) â†¦ (cod uâ‚â€²)` factors `u` into `uâ‚ƒâ‚` and
  `(dom uâ‚‚â€²) â†¦ (cod uâ‚‚â€²)` factors `u` into `uâ‚ƒâ‚‚`.
  We will show that `(dom u) â†¦ (cod u)` factors `u` into `uâ‚ƒâ‚ âŠ” uâ‚ƒâ‚‚`.
  So we need to show that

        dom (uâ‚ƒâ‚ âŠ” uâ‚ƒâ‚‚) âŠ‘ dom (uâ‚â€² âŠ” uâ‚‚â€²)
        cod (uâ‚â€² âŠ” uâ‚‚â€²) âŠ‘ cod (uâ‚ƒâ‚ âŠ” uâ‚ƒâ‚‚)

  But those both follow directly from the factoring of
  `u` into `uâ‚ƒâ‚` and `uâ‚ƒâ‚‚`, using the monotonicity of `âŠ”` with respect to `âŠ‘`.


### Inversion of less-than for functions

We come to the proof of the main lemma concerning the inversion of
less-than for functions. We show that if `uâ‚ âŠ‘ uâ‚‚`, then for any
`v â†¦ w âˆˆ uâ‚`, we can factor `uâ‚‚` into `uâ‚ƒ` according to `v â†¦ w`. We proceed
by induction on the derivation of `uâ‚ âŠ‘ uâ‚‚`, and describe each case in
the text after the Agda proof.

```
sub-inv : âˆ€{uâ‚ uâ‚‚ : Value}
        â†’ uâ‚ âŠ‘ uâ‚‚
        â†’ âˆ€{v w} â†’ v â†¦ w âˆˆ uâ‚
          -------------------------------------
        â†’ Î£[ uâ‚ƒ âˆˆ Value ] factor uâ‚‚ uâ‚ƒ v w
sub-inv {âŠ¥} {uâ‚‚} âŠ‘-bot {v} {w} ()
sub-inv {uâ‚â‚ âŠ” uâ‚â‚‚} {uâ‚‚} (âŠ‘-conj-L lt1 lt2) {v} {w} (injâ‚ x) = sub-inv lt1 x
sub-inv {uâ‚â‚ âŠ” uâ‚â‚‚} {uâ‚‚} (âŠ‘-conj-L lt1 lt2) {v} {w} (injâ‚‚ y) = sub-inv lt2 y
sub-inv {uâ‚} {uâ‚‚â‚ âŠ” uâ‚‚â‚‚} (âŠ‘-conj-R1 lt) {v} {w} m
    with sub-inv lt m
... | âŸ¨ uâ‚ƒâ‚ , âŸ¨ fuâ‚ƒâ‚ , âŸ¨ uâ‚ƒâ‚âŠ†uâ‚‚â‚ , âŸ¨ domuâ‚ƒâ‚âŠ‘v , wâŠ‘coduâ‚ƒâ‚ âŸ© âŸ© âŸ© âŸ© =
      âŸ¨ uâ‚ƒâ‚ , âŸ¨ fuâ‚ƒâ‚ , âŸ¨ (Î» {w} z â†’ injâ‚ (uâ‚ƒâ‚âŠ†uâ‚‚â‚ z)) ,
                                   âŸ¨ domuâ‚ƒâ‚âŠ‘v , wâŠ‘coduâ‚ƒâ‚ âŸ© âŸ© âŸ© âŸ©
sub-inv {uâ‚} {uâ‚‚â‚ âŠ” uâ‚‚â‚‚} (âŠ‘-conj-R2 lt) {v} {w} m
    with sub-inv lt m
... | âŸ¨ uâ‚ƒâ‚‚ , âŸ¨ fuâ‚ƒâ‚‚ , âŸ¨ uâ‚ƒâ‚‚âŠ†uâ‚‚â‚‚ , âŸ¨ domuâ‚ƒâ‚‚âŠ‘v , wâŠ‘coduâ‚ƒâ‚‚ âŸ© âŸ© âŸ© âŸ© =
      âŸ¨ uâ‚ƒâ‚‚ , âŸ¨ fuâ‚ƒâ‚‚ , âŸ¨ (Î» {C} z â†’ injâ‚‚ (uâ‚ƒâ‚‚âŠ†uâ‚‚â‚‚ z)) ,
                                   âŸ¨ domuâ‚ƒâ‚‚âŠ‘v , wâŠ‘coduâ‚ƒâ‚‚ âŸ© âŸ© âŸ© âŸ©
sub-inv {uâ‚} {uâ‚‚} (âŠ‘-trans{v = u} uâ‚âŠ‘u uâŠ‘uâ‚‚) {v} {w} vâ†¦wâˆˆuâ‚
    with sub-inv uâ‚âŠ‘u vâ†¦wâˆˆuâ‚
... | âŸ¨ uâ€² , âŸ¨ fuâ€² , âŸ¨ uâ€²âŠ†u , âŸ¨ domuâ€²âŠ‘v , wâŠ‘coduâ€² âŸ© âŸ© âŸ© âŸ©
    with sub-inv-trans {uâ€²} fuâ€² uâ€²âŠ†u (sub-inv uâŠ‘uâ‚‚)
... | âŸ¨ uâ‚ƒ , âŸ¨ fuâ‚ƒ , âŸ¨ uâ‚ƒâŠ†uâ‚‚ , âŸ¨ domuâ‚ƒâŠ‘domuâ€² , coduâ€²âŠ‘coduâ‚ƒ âŸ© âŸ© âŸ© âŸ© =
      âŸ¨ uâ‚ƒ , âŸ¨ fuâ‚ƒ , âŸ¨ uâ‚ƒâŠ†uâ‚‚ , âŸ¨ âŠ‘-trans domuâ‚ƒâŠ‘domuâ€² domuâ€²âŠ‘v ,
                                    âŠ‘-trans wâŠ‘coduâ€² coduâ€²âŠ‘coduâ‚ƒ âŸ© âŸ© âŸ© âŸ©
sub-inv {uâ‚â‚ â†¦ uâ‚â‚‚} {uâ‚‚â‚ â†¦ uâ‚‚â‚‚} (âŠ‘-fun lt1 lt2) refl =
    âŸ¨ uâ‚‚â‚ â†¦ uâ‚‚â‚‚ , âŸ¨ (Î» {w} â†’ fun) , âŸ¨ (Î» {C} z â†’ z) , âŸ¨ lt1 , lt2 âŸ© âŸ© âŸ© âŸ©
sub-inv {uâ‚‚â‚ â†¦ (uâ‚‚â‚‚ âŠ” uâ‚‚â‚ƒ)} {uâ‚‚â‚ â†¦ uâ‚‚â‚‚ âŠ” uâ‚‚â‚ â†¦ uâ‚‚â‚ƒ} âŠ‘-dist
    {.uâ‚‚â‚} {.(uâ‚‚â‚‚ âŠ” uâ‚‚â‚ƒ)} refl =
    âŸ¨ uâ‚‚â‚ â†¦ uâ‚‚â‚‚ âŠ” uâ‚‚â‚ â†¦ uâ‚‚â‚ƒ , âŸ¨ f , âŸ¨ g , âŸ¨ âŠ‘-conj-L âŠ‘-refl âŠ‘-refl , âŠ‘-refl âŸ© âŸ© âŸ© âŸ©
  where f : all-funs (uâ‚‚â‚ â†¦ uâ‚‚â‚‚ âŠ” uâ‚‚â‚ â†¦ uâ‚‚â‚ƒ)
        f (injâ‚ x) = fun x
        f (injâ‚‚ y) = fun y
        g : (uâ‚‚â‚ â†¦ uâ‚‚â‚‚ âŠ” uâ‚‚â‚ â†¦ uâ‚‚â‚ƒ) âŠ† (uâ‚‚â‚ â†¦ uâ‚‚â‚‚ âŠ” uâ‚‚â‚ â†¦ uâ‚‚â‚ƒ)
        g (injâ‚ x) = injâ‚ x
        g (injâ‚‚ y) = injâ‚‚ y
```

Let `v` and `w` be arbitrary values.

* Case `âŠ‘-bot`. So `uâ‚ â‰¡ âŠ¥`. We have `v â†¦ w âˆˆ âŠ¥`, but that is impossible.

* Case `âŠ‘-conj-L`.

        uâ‚â‚ âŠ‘ uâ‚‚   uâ‚â‚‚ âŠ‘ uâ‚‚
        -------------------
        uâ‚â‚ âŠ” uâ‚â‚‚ âŠ‘ uâ‚‚

  Given that `v â†¦ w âˆˆ uâ‚â‚ âŠ” uâ‚â‚‚`, there are two subcases to consider.

  * Subcase `v â†¦ w âˆˆ uâ‚â‚`. We conclude by the induction
    hypothesis for `uâ‚â‚ âŠ‘ uâ‚‚`.

  * Subcase `v â†¦ w âˆˆ uâ‚â‚‚`. We conclude by the induction hypothesis
    for `uâ‚â‚‚ âŠ‘ uâ‚‚`.

* Case `âŠ‘-conj-R1`.

        uâ‚ âŠ‘ uâ‚‚â‚
        --------------
        uâ‚ âŠ‘ uâ‚‚â‚ âŠ” uâ‚‚â‚‚

  Given that `v â†¦ w âˆˆ uâ‚`, the induction hypothesis for `uâ‚ âŠ‘ uâ‚‚â‚`
  gives us that `v â†¦ w` factors `uâ‚‚â‚` into `uâ‚ƒâ‚` for some `uâ‚ƒâ‚`.
  To show that `v â†¦ w` also factors `uâ‚‚â‚ âŠ” uâ‚‚â‚‚` into `uâ‚ƒâ‚`,
  we just need to show that `uâ‚ƒâ‚ âŠ† uâ‚‚â‚ âŠ” uâ‚‚â‚‚`, but that follows
  directly from `uâ‚ƒâ‚ âŠ† uâ‚‚â‚`.

* Case `âŠ‘-conj-R2`. This case follows by reasoning similar to
  the case for `âŠ‘-conj-R1`.

* Case `âŠ‘-trans`.

        uâ‚ âŠ‘ u   u âŠ‘ uâ‚‚
        ---------------
            uâ‚ âŠ‘ uâ‚‚

  By the induction hypothesis for `uâ‚ âŠ‘ u`, we know
  that `v â†¦ w` factors `u` into `uâ€²`, for some value `uâ€²`,
  so we have `all-funs uâ€²` and `uâ€² âŠ† u`.
  By the induction hypothesis for `u âŠ‘ uâ‚‚`, we know
  that for any `vâ€² â†¦ wâ€² âˆˆ u`, `vâ€² â†¦ wâ€²` factors `uâ‚‚`.
  Now we apply the lemma sub-inv-trans, which gives us
  some `uâ‚ƒ` such that `(dom uâ€²) â†¦ (cod uâ€²)` factors `uâ‚‚` into `uâ‚ƒ`.
  We show that `v â†¦ w` also factors `uâ‚‚` into `uâ‚ƒ`.
  From `dom uâ‚ƒ âŠ‘ dom uâ€²` and `dom uâ€² âŠ‘ v`, we have `dom uâ‚ƒ âŠ‘ v`.
  From `w âŠ‘ cod uâ€²` and `cod uâ€² âŠ‘ cod uâ‚ƒ`, we have `w âŠ‘ cod uâ‚ƒ`,
  and this case is complete.

* Case `âŠ‘-fun`.

        uâ‚‚â‚ âŠ‘ uâ‚â‚  uâ‚â‚‚ âŠ‘ uâ‚‚â‚‚
        ---------------------
        uâ‚â‚ â†¦ uâ‚â‚‚ âŠ‘ uâ‚‚â‚ â†¦ uâ‚‚â‚‚

  Given that `v â†¦ w âˆˆ uâ‚â‚ â†¦ uâ‚â‚‚`, we have `v â‰¡ uâ‚â‚` and `w â‰¡ uâ‚â‚‚`.
  We show that `uâ‚â‚ â†¦ uâ‚â‚‚` factors `uâ‚‚â‚ â†¦ uâ‚‚â‚‚` into itself.
  We need to show that `dom (uâ‚‚â‚ â†¦ uâ‚‚â‚‚) âŠ‘ uâ‚â‚` and `uâ‚â‚‚ âŠ‘ cod (uâ‚‚â‚ â†¦ uâ‚‚â‚‚)`,
  but that is equivalent to our premises `uâ‚‚â‚ âŠ‘ uâ‚â‚` and `uâ‚â‚‚ âŠ‘ uâ‚‚â‚‚`.

* Case `âŠ‘-dist`.

        ---------------------------------------------
        uâ‚‚â‚ â†¦ (uâ‚‚â‚‚ âŠ” uâ‚‚â‚ƒ) âŠ‘ (uâ‚‚â‚ â†¦ uâ‚‚â‚‚) âŠ” (uâ‚‚â‚ â†¦ uâ‚‚â‚ƒ)

  Given that `v â†¦ w âˆˆ uâ‚‚â‚ â†¦ (uâ‚‚â‚‚ âŠ” uâ‚‚â‚ƒ)`, we have `v â‰¡ uâ‚‚â‚`
  and `w â‰¡ uâ‚‚â‚‚ âŠ” uâ‚‚â‚ƒ`.
  We show that `uâ‚‚â‚ â†¦ (uâ‚‚â‚‚ âŠ” uâ‚‚â‚ƒ)` factors `(uâ‚‚â‚ â†¦ uâ‚‚â‚‚) âŠ” (uâ‚‚â‚ â†¦ uâ‚‚â‚ƒ)`
  into itself. We have `uâ‚‚â‚ âŠ” uâ‚‚â‚ âŠ‘ uâ‚‚â‚`, and also
  `uâ‚‚â‚‚ âŠ” uâ‚‚â‚ƒ âŠ‘ uâ‚‚â‚‚ âŠ” uâ‚‚â‚ƒ`, so the proof is complete.


We conclude this section with two corollaries of the sub-inv lemma.
First, we have the following property that is convenient to use in
later proofs. We specialize the premise to just `v â†¦ w âŠ‘ uâ‚`
and we modify the conclusion to say that for every
`vâ€² â†¦ wâ€² âˆˆ uâ‚‚`, we have `vâ€² âŠ‘ v`.

```
sub-inv-fun : âˆ€{v w uâ‚ : Value}
    â†’ (v â†¦ w) âŠ‘ uâ‚
      -----------------------------------------------------
    â†’ Î£[ uâ‚‚ âˆˆ Value ] all-funs uâ‚‚ Ã— uâ‚‚ âŠ† uâ‚
        Ã— (âˆ€{vâ€² wâ€²} â†’ (vâ€² â†¦ wâ€²) âˆˆ uâ‚‚ â†’ vâ€² âŠ‘ v) Ã— w âŠ‘ cod uâ‚‚
sub-inv-fun{v}{w}{uâ‚} abc
    with sub-inv abc {v}{w} refl
... | âŸ¨ uâ‚‚ , âŸ¨ f , âŸ¨ uâ‚‚âŠ†uâ‚ , âŸ¨ db , cc âŸ© âŸ© âŸ© âŸ© =
      âŸ¨ uâ‚‚ , âŸ¨ f , âŸ¨ uâ‚‚âŠ†uâ‚ , âŸ¨ G , cc âŸ© âŸ© âŸ© âŸ©
   where G : âˆ€{D E} â†’ (D â†¦ E) âˆˆ uâ‚‚ â†’ D âŠ‘ v
         G{D}{E} m = âŠ‘-trans (âŠ†â†’âŠ‘ (â†¦âˆˆâ†’âŠ†dom f m)) db
```

The second corollary is the inversion rule that one would expect for
less-than with functions on the left and right-hand sides.

```
â†¦âŠ‘â†¦-inv : âˆ€{v w vâ€² wâ€²}
        â†’ v â†¦ w âŠ‘ vâ€² â†¦ wâ€²
          -----------------
        â†’ vâ€² âŠ‘ v Ã— w âŠ‘ wâ€²
â†¦âŠ‘â†¦-inv{v}{w}{vâ€²}{wâ€²} lt
    with sub-inv-fun lt
... | âŸ¨ Î“ , âŸ¨ f , âŸ¨ Î“âŠ†v34 , âŸ¨ lt1 , lt2 âŸ© âŸ© âŸ© âŸ©
    with all-funsâˆˆ f
... | âŸ¨ u , âŸ¨ uâ€² , uâ†¦uâ€²âˆˆÎ“ âŸ© âŸ©
    with Î“âŠ†v34 uâ†¦uâ€²âˆˆÎ“
... | refl =
  let codÎ“âŠ†wâ€² = âŠ†â†¦â†’codâŠ† Î“âŠ†v34 in
  âŸ¨ lt1 uâ†¦uâ€²âˆˆÎ“ , âŠ‘-trans lt2 (âŠ†â†’âŠ‘ codÎ“âŠ†wâ€²) âŸ©
```


## Notes

The denotational semantics presented in this chapter is an example of
a _filter model_ (Barendregt, Coppo, Dezani-Ciancaglini, 1983). Filter
models use type systems with intersection types to precisely
characterize runtime behavior (Coppo, Dezani-Ciancaglini, and Salle,
1979). The notation that we use in this chapter is not that of type
systems and intersection types, but the `Value` data type is isomorphic
to types (`â†¦` is `â†’`, `âŠ”` is `âˆ§`, `âŠ¥` is `âŠ¤`), the `âŠ‘` relation is the
inverse of subtyping `<:`, and the evaluation relation `Ï âŠ¢ M â†“ v` is
isomorphic to a type system. Write `Î“` instead of `Ï`, `A` instead of `v`, and
replace `â†“` with `:` and one has a typing judgement `Î“ âŠ¢ M : A`.
By varying the definition of subtyping and using different choices of
type atoms, intersection type systems provide semantics for many different
untyped Î» calculi, from full beta to the lazy and call-by-value calculi
(Alessi, Barbanera, and Dezani-Ciancaglini, 2006) (Rocca and Paolini, 2004).
The denotational semantics in this chapter corresponds to the BCD
system (Barendregt, Coppo, Dezani-Ciancaglini, 1983).  Part 3 of the
book _Lambda Calculus with Types_ describes a framework for
intersection type systems that enables results similar to the ones in
this chapter, but for the entire family of intersection type systems
(Barendregt, Dekkers, and Statman, 2013).

The two ideas of using finite tables to represent functions and of
relaxing table lookup to enable self application first appeared in a
technical report by Gordon Plotkin (1972) and are later described in
an article in Theoretical Computer Science (Plotkin 1993).  In that
work, the inductive definition of `Value` is a bit different than the
one we use:

    Value = C + â„˜f(Value) Ã— â„˜f(Value)

where `C` is a set of constants and `â„˜f` means finite powerset.  The
pairs in `â„˜f(Value) Ã— â„˜f(Value)` represent input-output mappings, just
as in this chapter. The finite powersets are used to enable a function
table to appear in the input and in the output. These differences
amount to changing where the recursion appears in the definition of
`Value`.  Plotkin's model is an example of a _graph model_ of the
untyped lambda calculus (Barendregt, 1984). In a graph model, the
semantics is presented as a function from programs and environments to
(possibly infinite) sets of values. The semantics in this chapter is
instead defined as a relation, but set-valued functions are isomorphic
to relations. Indeed, we present the semantics as a function in the
next chapter and prove that it is equivalent to the relational
version.

Dana Scott's â„˜(Ï‰) (1976) and Engeler's B(A) (1981) are two more
examples of graph models. Both use the following inductive definition
of `Value`.

    Value = C + â„˜f(Value) Ã— Value

The use of `Value` instead of `â„˜f(Value)` in the output does not restrict
expressiveness compared to Plotkin's model because the semantics use
sets of values and a pair of sets `(V, Vâ€²)` can be represented as a set
of pairs `{ (V, vâ€²) | vâ€² âˆˆ Vâ€² }`.  In Scott's â„˜(Ï‰), the above values are
mapped to and from the natural numbers using a kind of Godel encoding.


## References

* Intersection Types and Lambda Models.  Fabio Alessi, Franco
  Barbanera, and Mariangiola Dezani-Ciancaglini, Theoretical
  Compututer Science, vol. 355, pages 108-126, 2006.

* The Lambda Calculus. H.P. Barendregt, 1984.

* A filter lambda model and the completeness of type assignment.  Henk
  Barendregt, Mario Coppo, and Mariangiola Dezani-Ciancaglini, Journal
  of Symbolic Logic, vol. 48, pages 931-940, 1983.

* Lambda Calculus with Types. Henk Barendregt, Wil Dekkers, and
  Richard Statman, Cambridge University Press, Perspectives in Logic,
  2013.

* Functional characterization of some semantic equalities inside
  Î»-calculus. Mario Coppo, Mariangiola Dezani-Ciancaglini, and Patrick
  Salle, in Sixth Colloquium on Automata, Languages and Programming.
  Springer, pages 133--146, 1979.

* Algebras and combinators. Erwin Engeler, Algebra Universalis,
  vol. 13, pages 389-392, 1981.

* A Set-Theoretical Definition of Application. Gordon D. Plotkin,
  University of Edinburgh, Technical Report MIP-R-95, 1972.

* Set-theoretical and other elementary models of the Î»-calculus.
  Gordon D. Plotkin, Theoretical Computer Science, vol. 121,
  pages 351-409, 1993.

* The Parametric Lambda Calculus. Simona Ronchi Della Rocca and Luca
  Paolini, Springer, 2004.

* Data Types as Lattices. Dana Scott, SIAM Journal on Computing,
  vol. 5, pages 522-587, 1976.

## Unicode

This chapter uses the following unicode:

    âŠ¥  U+22A5  UP TACK (\bot)
    â†¦  U+21A6  RIGHTWARDS ARROW FROM BAR (\mapsto)
    âŠ”  U+2294  SQUARE CUP (\lub)
    âŠ‘  U+2291  SQUARE IMAGE OF OR EQUAL TO (\sqsubseteq)
    âŠ¢  U+22A2  RIGHT TACK (\|- or \vdash)
    â†“  U+2193  DOWNWARDS ARROW (\d)
    á¶œ  U+1D9C  MODIFIER LETTER SMALL C (\^c)
    â„°  U+2130  SCRIPT CAPITAL E (\McE)
    â‰ƒ  U+2243  ASYMPTOTICALLY EQUAL TO (\~- or \simeq)
    âˆˆ  U+2208  ELEMENT OF (\in)
    âŠ†  U+2286  SUBSET OF OR EQUAL TO (\sub= or \subseteq)
