---
title     : "BigStep: Big-step semantics of untyped lambda calculus"
layout    : page
prev      : /Confluence/
permalink : /BigStep/
next      : /Denotational/
---

```
module plfa.part2.BigStep where
```

## Introduction

The call-by-name evaluation strategy is a deterministic method for
computing the value of a program in the lambda calculus.  That is,
call-by-name produces a value if and only if beta reduction can reduce
the program to a lambda abstraction. In this chapter we define
call-by-name evaluation and prove the forward direction of this
if-and-only-if. The backward direction is traditionally proved via
Curry-Feys standardisation, which is quite complex.  We give a sketch
of that proof, due to Plotkin, but postpone the proof in Agda until
after we have developed a denotational semantics for the lambda
calculus, at which point the proof is an easy corollary of properties
of the denotational semantics.

We present the call-by-name strategy as a relation between an an input
term and an output value. Such a relation is often called a _big-step
semantics_, written `M â‡“ V`, as it relates the input term `M` directly
to the final result `V`, in contrast to the small-step reduction
relation, `M â€”â†’ Mâ€²`, that maps `M` to another term `Mâ€²` in which a
single sub-computation has been completed.

## Imports

```
open import Relation.Binary.PropositionalEquality
  using (_â‰¡_; refl; trans; sym; cong-app)
open import Data.Product using (_Ã—_; Î£; Î£-syntax; âˆƒ; âˆƒ-syntax; projâ‚; projâ‚‚)
  renaming (_,_ to âŸ¨_,_âŸ©)
open import Function using (_âˆ˜_)
open import plfa.part2.Untyped
  using (Context; _âŠ¢_; _âˆ‹_; â˜…; âˆ…; _,_; Z; S_; `_; #_; Æ›_; _Â·_;
  subst; subst-zero; exts; rename; Î²; Î¾â‚; Î¾â‚‚; Î¶; _â€”â†’_; _â€”â† _; _â€”â†’âŸ¨_âŸ©_; _âˆ;
  â€”â† -trans; appL-cong)
open import plfa.part2.Substitution using (Subst; ids)
```

## Environments

To handle variables and function application, there is the choice
between using substitution, as in `â€”â†’`, or to use an _environment_.
An environment in call-by-name is a map from variables to closures,
that is, to terms paired with their environments. We choose to use
environments instead of substitution because the point of the
call-by-name strategy is to be closer to an implementation of the
language. Also, the denotational semantics introduced in later
chapters uses environments and the proof of adequacy
is made easier by aligning these choices.

We define environments and closures as follows.

```
ClosEnv : Context â†’ Set

data Clos : Set where
  clos : âˆ€{Î“} â†’ (M : Î“ âŠ¢ â˜…) â†’ ClosEnv Î“ â†’ Clos

ClosEnv Î“ = âˆ€ (x : Î“ âˆ‹ â˜…) â†’ Clos
```

As usual, we have the empty environment, and we can extend an
environment.

```
âˆ…' : ClosEnv âˆ…
âˆ…' ()

_,'_ : âˆ€ {Î“} â†’ ClosEnv Î“ â†’ Clos â†’ ClosEnv (Î“ , â˜…)
(Î³ ,' c) Z = c
(Î³ ,' c) (S x) = Î³ x
```

## Big-step evaluation

The big-step semantics is represented as a ternary relation,
written `Î³ âŠ¢ M â‡“ V`, where `Î³` is the environment, `M` is the input
term, and `V` is the result value.  A _value_ is a closure whose term
is a lambda abstraction.

```
data _âŠ¢_â‡“_ : âˆ€{Î“} â†’ ClosEnv Î“ â†’ (Î“ âŠ¢ â˜…) â†’ Clos â†’ Set where

  â‡“-var : âˆ€{Î“}{Î³ : ClosEnv Î“}{x : Î“ âˆ‹ â˜…}{Î”}{Î´ : ClosEnv Î”}{M : Î” âŠ¢ â˜…}{V}
    â†’ Î³ x â‰¡ clos M Î´
    â†’ Î´ âŠ¢ M â‡“ V
      -----------
    â†’ Î³ âŠ¢ ` x â‡“ V

  â‡“-lam : âˆ€{Î“}{Î³ : ClosEnv Î“}{M : Î“ , â˜… âŠ¢ â˜…}
    â†’ Î³ âŠ¢ Æ› M â‡“ clos (Æ› M) Î³

  â‡“-app : âˆ€{Î“}{Î³ : ClosEnv Î“}{L M : Î“ âŠ¢ â˜…}{Î”}{Î´ : ClosEnv Î”}{N : Î” , â˜… âŠ¢ â˜…}{V}
    â†’ Î³ âŠ¢ L â‡“ clos (Æ› N) Î´   â†’   (Î´ ,' clos M Î³) âŠ¢ N â‡“ V
      ---------------------------------------------------
    â†’ Î³ âŠ¢ L Â· M â‡“ V
```

* The `â‡“-var` rule evaluates a variable by finding the associated
  closure in the environment and then evaluating the closure.

* The `â‡“-lam` rule turns a lambda abstraction into a closure
  by packaging it up with its environment.

* The `â‡“-app` rule performs function application by first evaluating
  the term `L` in operator position. If that produces a closure containing
  a lambda abstraction `Æ› N`, then we evaluate the body `N` in an
  environment extended with the argument `M`. Note that `M` is not
  evaluated in rule `â‡“-app` because this is call-by-name and not
  call-by-value.


#### Exercise `big-step-eg` (practice)

Show that `(Æ› Æ› # 1) Â· ((Æ› # 0 Â· # 0) Â· (Æ› # 0 Â· # 0))`
terminates under big-step call-by-name evaluation.

```
-- Your code goes here
```


## The big-step semantics is deterministic

If the big-step relation evaluates a term `M` to both `V` and
`Vâ€²`, then `V` and `Vâ€²` must be identical. In other words, the
call-by-name relation is a partial function. The proof is a
straightforward induction on the two big-step derivations.

```
â‡“-determ : âˆ€{Î“}{Î³ : ClosEnv Î“}{M : Î“ âŠ¢ â˜…}{V V' : Clos}
  â†’ Î³ âŠ¢ M â‡“ V â†’ Î³ âŠ¢ M â‡“ V'
  â†’ V â‰¡ V'
â‡“-determ (â‡“-var eq1 mc) (â‡“-var eq2 mc')
    with trans (sym eq1) eq2
... | refl = â‡“-determ mc mc'
â‡“-determ â‡“-lam â‡“-lam = refl
â‡“-determ (â‡“-app mc mcâ‚) (â‡“-app mc' mc'')
    with â‡“-determ mc mc'
... | refl = â‡“-determ mcâ‚ mc''
```


## Big-step evaluation implies beta reduction to a lambda

If big-step evaluation produces a value, then the input term can
reduce to a lambda abstraction by beta reduction:

      âˆ…' âŠ¢ M â‡“ clos (Æ› Nâ€²) Î´
      -----------------------------
    â†’ Î£[ N âˆˆ âˆ… , â˜… âŠ¢ â˜… ] (M â€”â†  Æ› N)

The proof is by induction on the big-step derivation. As is often
necessary, one must generalize the statement to get the induction to
go through. In the case for `â‡“-app` (function application), the
argument is added to the environment, so the environment becomes
non-empty. The corresponding Î² reduction substitutes the argument into
the body of the lambda abstraction.  So we generalize the lemma to
allow an arbitrary environment `Î³` and we add a premise that relates
the environment `Î³` to an equivalent substitution `Ïƒ`.

The case for `â‡“-app` also requires that we strengthen the
conclusion. In the case for `â‡“-app` we have `Î³ âŠ¢ L â‡“ clos (Î» N) Î´` and
the induction hypothesis gives us `L â€”â†  Æ› Nâ€²`, but we need to know
that `N` and `Nâ€²` are equivalent. In particular, that `N â‰¡ subst Ï„ Nâ€²`
where `Ï„` is the substitution that is equivalent to `Î´`. Therefore we
expand the conclusion of the statement, stating that the results are
equivalent.

We make the two notions of equivalence precise by defining the
following two mutually-recursive predicates `V â‰ˆ M` and `Î³ â‰ˆâ‚‘ Ïƒ`.

```
_â‰ˆ_ : Clos â†’ (âˆ… âŠ¢ â˜…) â†’ Set
_â‰ˆâ‚‘_ : âˆ€{Î“} â†’ ClosEnv Î“ â†’ Subst Î“ âˆ… â†’ Set

(clos {Î“} M Î³) â‰ˆ N = Î£[ Ïƒ âˆˆ Subst Î“ âˆ… ] Î³ â‰ˆâ‚‘ Ïƒ Ã— (N â‰¡ subst Ïƒ M)

Î³ â‰ˆâ‚‘ Ïƒ = âˆ€{x} â†’ (Î³ x) â‰ˆ (Ïƒ x)
```

We can now state the main lemma:

    If Î³ âŠ¢ M â‡“ V  and  Î³ â‰ˆâ‚‘ Ïƒ,
    then  subst Ïƒ M â€”â†  N  and  V â‰ˆ N  for some N.

Before starting the proof, we establish a couple lemmas
about equivalent environments and substitutions.

The empty environment is equivalent to the identity substitution
`ids`, which we import from Chapter [Substitution]({{ site.baseurl
}}/Substitution/).

```
â‰ˆâ‚‘-id : âˆ…' â‰ˆâ‚‘ ids
â‰ˆâ‚‘-id {()}
```

Of course, applying the identity substitution to a term returns
the same term.

```
sub-id : âˆ€{Î“} {A} {M : Î“ âŠ¢ A} â†’ subst ids M â‰¡ M
sub-id = plfa.part2.Substitution.sub-id
```


We define an auxilliary function for extending a substitution.

```
ext-subst : âˆ€{Î“ Î”} â†’ Subst Î“ Î” â†’ Î” âŠ¢ â˜… â†’ Subst (Î“ , â˜…) Î”
ext-subst{Î“}{Î”} Ïƒ N {A} = subst (subst-zero N) âˆ˜ exts Ïƒ
```

The next lemma we need to prove states that if you start with an
equivalent environment and substitution `Î³ â‰ˆâ‚‘ Ïƒ`, extending them with
an equivalent closure and term `c â‰ˆ N` produces an equivalent
environment and substitution: `(Î³ ,' V) â‰ˆâ‚‘ (ext-subst Ïƒ N)`,
or equivalently, `(Î³ ,' V) x â‰ˆâ‚‘ (ext-subst Ïƒ N) x` for any
variable `x`. The proof will be by induction on `x` and
for the induction step we need the following lemma,
which states that applying the composition of `exts Ïƒ`
and `subst-zero` to `S x` is the same as just `Ïƒ x`,
which is a corollary of a theorem in
Chapter [Substitution]({{ site.baseurl }}/Substitution/).

```
subst-zero-exts : âˆ€{Î“ Î”}{Ïƒ : Subst Î“ Î”}{B}{M : Î” âŠ¢ B}{x : Î“ âˆ‹ â˜…}
  â†’ (subst (subst-zero M) âˆ˜ exts Ïƒ) (S x) â‰¡ Ïƒ x
subst-zero-exts {Î“}{Î”}{Ïƒ}{B}{M}{x} =
   cong-app (plfa.part2.Substitution.subst-zero-exts-cons{Ïƒ = Ïƒ}) (S x)
```

So the proof of `â‰ˆâ‚‘-ext` is as follows.

```
â‰ˆâ‚‘-ext : âˆ€ {Î“} {Î³ : ClosEnv Î“} {Ïƒ : Subst Î“ âˆ…} {V} {N : âˆ… âŠ¢ â˜…}
  â†’ Î³ â‰ˆâ‚‘ Ïƒ  â†’  V â‰ˆ N
    --------------------------
  â†’ (Î³ ,' V) â‰ˆâ‚‘ (ext-subst Ïƒ N)
â‰ˆâ‚‘-ext {Î“} {Î³} {Ïƒ} {V} {N} Î³â‰ˆâ‚‘Ïƒ Vâ‰ˆN {Z} = Vâ‰ˆN
â‰ˆâ‚‘-ext {Î“} {Î³} {Ïƒ} {V} {N} Î³â‰ˆâ‚‘Ïƒ Vâ‰ˆN {S x}
  rewrite subst-zero-exts {Ïƒ = Ïƒ}{M = N}{x} = Î³â‰ˆâ‚‘Ïƒ
```

We proceed by induction on the input variable.

* If it is `Z`, then we immediately conclude using the
  premise `V â‰ˆ N`.

* If it is `S x`, then we rewrite using the
  `subst-zero-exts` lemma and use the premise `Î³ â‰ˆâ‚‘ Ïƒ`
  to conclude.


To prove the main lemma, we need another technical lemma about
substitution. Applying one substitution after another is the same as
composing the two substitutions and then applying them.

```
sub-sub : âˆ€{Î“ Î” Î£}{A}{M : Î“ âŠ¢ A} {Ïƒâ‚ : Subst Î“ Î”}{Ïƒâ‚‚ : Subst Î” Î£}
  â†’ subst Ïƒâ‚‚ (subst Ïƒâ‚ M) â‰¡ subst (subst Ïƒâ‚‚ âˆ˜ Ïƒâ‚) M
sub-sub {M = M} = plfa.part2.Substitution.sub-sub {M = M}
```

We arive at the main lemma: if `M` big steps to a
closure `V` in environment `Î³`, and if `Î³ â‰ˆâ‚‘ Ïƒ`, then `subst Ïƒ M` reduces
to some term `N` that is equivalent to `V`. We describe the proof
below.

```
â‡“â†’â€”â† Ã—ğ”¹ : âˆ€{Î“}{Î³ : ClosEnv Î“}{Ïƒ : Subst Î“ âˆ…}{M : Î“ âŠ¢ â˜…}{V : Clos}
       â†’ Î³ âŠ¢ M â‡“ V  â†’  Î³ â‰ˆâ‚‘ Ïƒ
         ---------------------------------------
       â†’ Î£[ N âˆˆ âˆ… âŠ¢ â˜… ] (subst Ïƒ M â€”â†  N) Ã— V â‰ˆ N
â‡“â†’â€”â† Ã—ğ”¹ {Î³ = Î³} (â‡“-var{x = x} Î³xâ‰¡LÎ´ Î´âŠ¢Lâ‡“V) Î³â‰ˆâ‚‘Ïƒ
    with Î³ x | Î³â‰ˆâ‚‘Ïƒ {x} | Î³xâ‰¡LÎ´
... | clos L Î´ | âŸ¨ Ï„ , âŸ¨ Î´â‰ˆâ‚‘Ï„ , Ïƒxâ‰¡Ï„L âŸ© âŸ© | refl
      with â‡“â†’â€”â† Ã—ğ”¹{Ïƒ = Ï„} Î´âŠ¢Lâ‡“V Î´â‰ˆâ‚‘Ï„
...   | âŸ¨ N , âŸ¨ Ï„Lâ€”â† N , Vâ‰ˆN âŸ© âŸ© rewrite Ïƒxâ‰¡Ï„L =
        âŸ¨ N , âŸ¨ Ï„Lâ€”â† N , Vâ‰ˆN âŸ© âŸ©
â‡“â†’â€”â† Ã—ğ”¹ {Ïƒ = Ïƒ} {V = clos (Æ› N) Î³} â‡“-lam Î³â‰ˆâ‚‘Ïƒ =
    âŸ¨ subst Ïƒ (Æ› N) , âŸ¨ subst Ïƒ (Æ› N) âˆ , âŸ¨ Ïƒ , âŸ¨ Î³â‰ˆâ‚‘Ïƒ , refl âŸ© âŸ© âŸ© âŸ©
â‡“â†’â€”â† Ã—ğ”¹{Î“}{Î³} {Ïƒ = Ïƒ} {L Â· M} {V} (â‡“-app {N = N} Lâ‡“Æ›NÎ´ Nâ‡“V) Î³â‰ˆâ‚‘Ïƒ
    with â‡“â†’â€”â† Ã—ğ”¹{Ïƒ = Ïƒ} Lâ‡“Æ›NÎ´ Î³â‰ˆâ‚‘Ïƒ
... | âŸ¨ _ , âŸ¨ ÏƒLâ€”â† Æ›Ï„N , âŸ¨ Ï„ , âŸ¨ Î´â‰ˆâ‚‘Ï„ , â‰¡Æ›Ï„N âŸ© âŸ© âŸ© âŸ© rewrite â‰¡Æ›Ï„N
      with â‡“â†’â€”â† Ã—ğ”¹ {Ïƒ = ext-subst Ï„ (subst Ïƒ M)} Nâ‡“V
             (Î» {x} â†’ â‰ˆâ‚‘-ext{Ïƒ = Ï„} Î´â‰ˆâ‚‘Ï„ âŸ¨ Ïƒ , âŸ¨ Î³â‰ˆâ‚‘Ïƒ , refl âŸ© âŸ© {x})
           | Î²{âˆ…}{subst (exts Ï„) N}{subst Ïƒ M}
...   | âŸ¨ N' , âŸ¨ â€”â† N' , Vâ‰ˆN' âŸ© âŸ© | Æ›Ï„NÂ·ÏƒMâ€”â†’
        rewrite sub-sub{M = N}{Ïƒâ‚ = exts Ï„}{Ïƒâ‚‚ = subst-zero (subst Ïƒ M)} =
        let rs = (Æ› subst (exts Ï„) N) Â· subst Ïƒ M â€”â†’âŸ¨ Æ›Ï„NÂ·ÏƒMâ€”â†’ âŸ© â€”â† N' in
        let g = â€”â† -trans (appL-cong ÏƒLâ€”â† Æ›Ï„N) rs in
        âŸ¨ N' , âŸ¨ g , Vâ‰ˆN' âŸ© âŸ©
```

The proof is by induction on `Î³ âŠ¢ M â‡“ V`. We have three cases
to consider.

* Case `â‡“-var`.
  So we have `Î³ x â‰¡ clos L Î´` and `Î´ âŠ¢ L â‡“ V`.
  We need to show that `subst Ïƒ x â€”â†  N` and `V â‰ˆ N` for some `N`.
  The premise `Î³ â‰ˆâ‚‘ Ïƒ` tells us that `Î³ x â‰ˆ Ïƒ x`, so `clos L Î´ â‰ˆ Ïƒ x`.
  By the definition of `â‰ˆ`, there exists a `Ï„` such that
  `Î´ â‰ˆâ‚‘ Ï„` and `Ïƒ x â‰¡ subst Ï„ L `.
  Using `Î´ âŠ¢ L â‡“ V` and `Î´ â‰ˆâ‚‘ Ï„`,
  the induction hypothesis gives us
  `subst Ï„ L â€”â†  N` and `V â‰ˆ N` for some `N`.
  So we have shown that `subst Ïƒ x â€”â†  N` and `V â‰ˆ N` for some `N`.

* Case `â‡“-lam`.
  We immediately have `subst Ïƒ (Æ› N) â€”â†  subst Ïƒ (Æ› N)`
  and `clos (subst Ïƒ (Æ› N)) Î³ â‰ˆ subst Ïƒ (Æ› N)`.

* Case `â‡“-app`.
  Using `Î³ âŠ¢ L â‡“ clos N Î´` and `Î³ â‰ˆâ‚‘ Ïƒ`,
  the induction hypothesis gives us

        subst Ïƒ L â€”â†  Æ› subst (exts Ï„) N                                     (1)

  and `Î´ â‰ˆâ‚‘ Ï„` for some `Ï„`.
  From `Î³â‰ˆâ‚‘Ïƒ` we have `clos M Î³ â‰ˆ subst Ïƒ M`.
  Then with `(Î´ ,' clos M Î³) âŠ¢ N â‡“ V`,
  the induction hypothesis gives us `V â‰ˆ N'` and

        subst (subst (subst-zero (subst Ïƒ M)) âˆ˜ (exts Ï„)) N â€”â†  N'         (2)

  Meanwhile, by `Î²`, we have

        (Æ› subst (exts Ï„) N) Â· subst Ïƒ M
        â€”â†’ subst (subst-zero (subst Ïƒ M)) (subst (exts Ï„) N)

  which is the same as the following, by `sub-sub`.

        (Æ› subst (exts Ï„) N) Â· subst Ïƒ M
        â€”â†’ subst (subst (subst-zero (subst Ïƒ M)) âˆ˜ exts Ï„) N              (3)

  Using (3) and (2) we have

        (Æ› subst (exts Ï„) N) Â· subst Ïƒ M â€”â†  N'                             (4)

  From (1) we have

        subst Ïƒ L Â· subst Ïƒ M â€”â†  (Æ› subst (exts Ï„) N) Â· subst Ïƒ M

  which we combine with (4) to conclude that

        subst Ïƒ L Â· subst Ïƒ M â€”â†  N'


With the main lemma complete, we establish the forward direction
of the equivalence between the big-step semantics and beta reduction.

```
cbnâ†’reduce :  âˆ€{M : âˆ… âŠ¢ â˜…}{Î”}{Î´ : ClosEnv Î”}{Nâ€² : Î” , â˜… âŠ¢ â˜…}
  â†’ âˆ…' âŠ¢ M â‡“ clos (Æ› Nâ€²) Î´
    -----------------------------
  â†’ Î£[ N âˆˆ âˆ… , â˜… âŠ¢ â˜… ] (M â€”â†  Æ› N)
cbnâ†’reduce {M}{Î”}{Î´}{Nâ€²} Mâ‡“c
    with â‡“â†’â€”â† Ã—ğ”¹{Ïƒ = ids} Mâ‡“c â‰ˆâ‚‘-id
... | âŸ¨ N , âŸ¨ rs , âŸ¨ Ïƒ , âŸ¨ h , eq2 âŸ© âŸ© âŸ© âŸ© rewrite sub-id{M = M} | eq2 =
      âŸ¨ subst (exts Ïƒ) Nâ€² , rs âŸ©
```

#### Exercise `big-alt-implies-multi` (practice)

Formulate an alternative big-step semantics, of the form `M â†“ N`,
for call-by-name that uses substitution instead of environments.
Prove that `M â†“ N` implies `M â€”â†  N`.

```
-- Your code goes here
```

## Beta reduction to a lambda implies big-step evaluation

The proof of the backward direction, that beta reduction to a lambda
implies that the call-by-name semantics produces a result, is more
difficult to prove. The difficulty stems from reduction proceeding
underneath lambda abstractions via the `Î¶` rule. The call-by-name
semantics does not reduce under lambda, so a straightforward proof by
induction on the reduction sequence is impossible.  In the article
_Call-by-name, call-by-value, and the Î»-calculus_, Plotkin proves the
theorem in two steps, using two auxilliary reduction relations. The
first step uses a classic technique called Curry-Feys standardisation.
It relies on the notion of _standard reduction sequence_, which acts
as a half-way point between full beta reduction and call-by-name by
expanding call-by-name to also include reduction underneath
lambda. Plotkin proves that `M` reduces to `L` if and only if `M` is
related to `L` by a standard reduction sequence.

    Theorem 1 (Standardisation)
    `M â€”â†  L` if and only if `M` goes to `L` via a standard reduction sequence.

Plotkin then introduces _left reduction_, a small-step version of
call-by-name and uses the above theorem to prove that beta reduction
and left reduction are equivalent in the following sense.

    Corollary 1
    `M â€”â†  Æ› N` if and only if `M` goes to `Æ› Nâ€²`, for some `Nâ€²`, by left reduction.

The second step of the proof connects left reduction to call-by-name
evaluation.

    Theorem 2
    `M` left reduces to `Æ› N` if and only if `âŠ¢ M â‡“ Æ› N`.

(Plotkin's call-by-name evaluator uses substitution instead of
environments, which explains why the environment is omitted in `âŠ¢ M â‡“
Æ› N` in the above theorem statement.)

Putting Corollary 1 and Theorem 2 together, Plotkin proves that
call-by-name evaluation is equivalent to beta reduction.

    Corollary 2
    `M â€”â†  Æ› N` if and only if `âŠ¢ M â‡“ Æ› Nâ€²` for some `Nâ€²`.

Plotkin also proves an analogous result for the Î»áµ¥ calculus, relating
it to call-by-value evaluation. For a nice exposition of that proof,
we recommend Chapter 5 of _Semantics Engineering with PLT Redex_ by
Felleisen, Findler, and Flatt.

Instead of proving the backwards direction via standardisation, as
sketched above, we defer the proof until after we define a
denotational semantics for the lambda calculus, at which point the
proof of the backwards direction will fall out as a corollary to the
soundness and adequacy of the denotational semantics.


## Unicode

This chapter uses the following unicode:

    â‰ˆ  U+2248  ALMOST EQUAL TO (\~~ or \approx)
    â‚‘  U+2091  LATIN SUBSCRIPT SMALL LETTER E (\_e)
    âŠ¢  U+22A2  RIGHT TACK (\|- or \vdash)
    â‡“  U+21DB  DOWNWARDS DOUBLE ARROW (\d= or \Downarrow)
