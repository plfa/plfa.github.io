---
title     : "Call-by-name big-step evaluation"
layout    : page
prev      : /Confluence/
permalink : /CallByName/
next      : /Denotational/
---

\begin{code}
module plfa.CallByName where
\end{code}

## Introduction

The call-by-name evaluation strategy is a deterministic method for
computing the value of a program in the lambda calculus.  That is,
call-by-name produces a value if and only if beta reduction can reduce
the program to a lambda abstraction. In this chapter we define
call-by-name evaluation and prove the forward direction of this
if-and-only-if. We postpone the backward direction until after we have
developed a denotational semantics for the lambda calculus, at which
point the proof will be an easy corollary of properties of the
denotational semantics.

We present the call-by-name strategy as a relation between an an input
term and an output value. Such a relation is often called a _big-step
semantics_, as it relates the input term directly to the final result,
in contrast to the small-step reduction relation `â€”â†’` that maps a term
to another term in which a single sub-computation has been completed.

## Imports

\begin{code}
open import plfa.Untyped
  using (Context; _âŠ¢_; _âˆ‹_; â˜…; âˆ…; _,_; Z; S_; `_; Æ›_; _Â·_; subst; subst-zero;
         exts; rename)
open import plfa.LambdaReduction
  using (Î²; Î¾â‚; Î¾â‚‚; Î¶; _â€”â†’_; _â€”â† _; _â€”â†’âŸ¨_âŸ©_; _[]; â€”â† -trans; appL-cong)
open import plfa.Substitution
  using (Subst; âŸª_âŸ«; _â€¢_; _â¨Ÿ_; ids; sub-id; sub-sub; subst-zero-exts-cons)
import Relation.Binary.PropositionalEquality as Eq
open Eq using (_â‰¡_; refl; trans; sym)
open Eq.â‰¡-Reasoning using (begin_; _â‰¡âŸ¨âŸ©_; _â‰¡âŸ¨_âŸ©_; _âˆ)

open import Data.Product using (_Ã—_; Î£; Î£-syntax; âˆƒ; âˆƒ-syntax; projâ‚; projâ‚‚)
  renaming (_,_ to âŸ¨_,_âŸ©)
open import Function using (_âˆ˜_)
\end{code}

## Environments

To handle variables and function application, there is the choice
between using substitution, as in `â€”â†’`, or to use an _environment_.
An environment in call-by-name is a map from variables to closures,
that is, to terms paired with their environments. We choose to use
environments instead of substitution because the point of the
call-by-name strategy is to be closer to an implementation of the
language. Also, the denotational semantics introduced in later
chapters uses environments and the proof of adequacy
is made easier by aligning these choices.

We define environments and closures as follows.

\begin{code}
ClosEnv : Context â†’ Set

data Clos : Set where
  clos : âˆ€{Î“} â†’ (M : Î“ âŠ¢ â˜…) â†’ ClosEnv Î“ â†’ Clos

ClosEnv Î“ = âˆ€ (x : Î“ âˆ‹ â˜…) â†’ Clos
\end{code}

As usual, we have the empty environment, and we can extend an
environment.

\begin{code}
âˆ…' : ClosEnv âˆ…
âˆ…' ()

_,'_ : âˆ€ {Î“} â†’ ClosEnv Î“ â†’ Clos â†’ ClosEnv (Î“ , â˜…)
(Î³ ,' c) Z = c
(Î³ ,' c) (S x) = Î³ x
\end{code}

## Big-step evaluation

The big-step semantics is represented as a ternary relation,
written `Î³ âŠ¢ M â‡“ V`, where `Î³` is the environment, `M` is the input
term, and `V` is the result value.  A _value_ is a closure whose term
is a lambda abstraction.

\begin{code}
data _âŠ¢_â‡“_ : âˆ€{Î“} â†’ ClosEnv Î“ â†’ (Î“ âŠ¢ â˜…) â†’ Clos â†’ Set where

  â‡“-var : âˆ€{Î“}{Î³ : ClosEnv Î“}{x : Î“ âˆ‹ â˜…}{Î”}{Î´ : ClosEnv Î”}{M : Î” âŠ¢ â˜…}{V}
        â†’ Î³ x â‰¡ clos M Î´
        â†’ Î´ âŠ¢ M â‡“ V
          -----------
        â†’ Î³ âŠ¢ ` x â‡“ V

  â‡“-lam : âˆ€{Î“}{Î³ : ClosEnv Î“}{M : Î“ , â˜… âŠ¢ â˜…}
        â†’ Î³ âŠ¢ Æ› M â‡“ clos (Æ› M) Î³

  â‡“-app : âˆ€{Î“}{Î³ : ClosEnv Î“}{L M : Î“ âŠ¢ â˜…}{Î”}{Î´ : ClosEnv Î”}{N : Î” , â˜… âŠ¢ â˜…}{V}
       â†’ Î³ âŠ¢ L â‡“ clos (Æ› N) Î´   â†’   (Î´ ,' clos M Î³) âŠ¢ N â‡“ V
         ---------------------------------------------------
       â†’ Î³ âŠ¢ L Â· M â‡“ V
\end{code}

* The `â‡“-var` rule evaluates a variable by finding the associated
  closure in the environment and then evaluating the closure.

* The `â‡“-lam` rule turns a lambda abstraction into a closure
  by packaging it up with its environment.

* The `â‡“-app` rule performs function application by first evaluating
  the term `L` in operator position. If that produces a closure containing
  a lambda abstraction `Æ› N`, then we evaluate the body `N` in an
  environment extended with the argument `M`. Note that `M` is not
  evaluated in rule `â‡“-app` because this is call-by-name and not
  call-by-value.


## The big-step semantics is deterministic

If the big-step relation evaluates a term `M` to both `V` and
`Vâ€²`, then `V` and `Vâ€²` must be identical. In other words, the
call-by-name relation is a partial function. The proof is a
straightforward induction on the two big-step derivations.

\begin{code}
â‡“-determ : âˆ€{Î“}{Î³ : ClosEnv Î“}{M : Î“ âŠ¢ â˜…}{V V' : Clos}
         â†’ Î³ âŠ¢ M â‡“ V â†’ Î³ âŠ¢ M â‡“ V'
         â†’ V â‰¡ V'
â‡“-determ (â‡“-var eq1 mc) (â‡“-var eq2 mc')
      with trans (sym eq1) eq2
... | refl = â‡“-determ mc mc'
â‡“-determ â‡“-lam â‡“-lam = refl
â‡“-determ (â‡“-app mc mcâ‚) (â‡“-app mc' mc'') 
    with â‡“-determ mc mc'
... | refl = â‡“-determ mcâ‚ mc''
\end{code}


## Big-step evaluation implies beta reduction to a lambda

If big-step evaluation produces a value, then the input term can
reduce to a lambda abstraction by beta reduction:

      âˆ…' âŠ¢ M â‡“ clos (Æ› Nâ€²) Î´
      -----------------------------
    â†’ Î£[ N âˆˆ âˆ… , â˜… âŠ¢ â˜… ] (M â€”â†  Æ› N)

The proof is by induction on the big-step derivation. As is often
necessary, one must generalize the statement to get the induction to
go through. In the case for `â‡“-app` (function application), the
argument is added to the environment, so the environment becomes
non-empty. The corresponding Î² reduction substitutes the argument into
the body of the lambda abstraction.  So we generalize the lemma to
allow an arbitrary environment `Î³` and we add a premise that relates
the environment `Î³` to an equivalent substitution `Ïƒ`.

The case for `â‡“-app` also requires that we strengthen the
conclusion. In the case for `â‡“-app` we have `Î³ âŠ¢ L â‡“ clos (Î» N) Î´` and
the induction hypothesis gives us `L â€”â†  Æ› Nâ€²`, but we need to know
that `N` and `Nâ€²` are equivalent. In particular, that `N â‰¡ subst Ï„ Nâ€²`
where `Ï„` is the substitution that is equivalent to `Î´`. Therefore we
expand the conclusion of the statement, stating that the results are
equivalent.

We make the two notions of equivalence precise by defining the
following two mutually-recursive predicates `c â‰ˆ M` and `Î³ â‰ˆâ‚‘ Ïƒ`.

\begin{code}
_â‰ˆ_ : Clos â†’ (âˆ… âŠ¢ â˜…) â†’ Set
_â‰ˆâ‚‘_ : âˆ€{Î“} â†’ ClosEnv Î“ â†’ Subst Î“ âˆ… â†’ Set

(clos {Î“} M Î³) â‰ˆ N = Î£[ Ïƒ âˆˆ Subst Î“ âˆ… ] Î³ â‰ˆâ‚‘ Ïƒ Ã— (N â‰¡ âŸª Ïƒ âŸ« M)

Î³ â‰ˆâ‚‘ Ïƒ = âˆ€{x} â†’ (Î³ x) â‰ˆ (Ïƒ x)
\end{code}

We can now state the main lemma:

    If Î³ âŠ¢ M â‡“ c  and  Î³ â‰ˆâ‚‘ Ïƒ,
    then  âŸª Ïƒ âŸ« M â€”â†  N  and  c â‰ˆ N  for some N.

Before starting the proof, we establish a couple lemmas
about equivalent environments and substitutions.

The empty environment is equivalent to the identity substitution.

\begin{code}
â‰ˆâ‚‘-id : âˆ…' â‰ˆâ‚‘ ids
â‰ˆâ‚‘-id {()}
\end{code}

We define an auxilliary function for extending a substitution.

\begin{code}
ext-subst : âˆ€{Î“ Î”} â†’ Subst Î“ Î” â†’ Î” âŠ¢ â˜… â†’ Subst (Î“ , â˜…) Î”
ext-subst{Î“}{Î”} Ïƒ N {A} = âŸª subst-zero N âŸ« âˆ˜ exts Ïƒ
\end{code}

The next lemma states that if you start with an equivalent
environment and substitution `Î³ â‰ˆâ‚‘ Ïƒ`, extending them with
an equivalent closure and term `c â‰ˆ N` produces
an equivalent environment and substitution:
`(Î³ ,' c) â‰ˆâ‚‘ (ext-subst Ïƒ N)`. 

\begin{code}
â‰ˆâ‚‘-ext : âˆ€ {Î“} {Î³ : ClosEnv Î“} {Ïƒ : Subst Î“ âˆ…} {c} {N : âˆ… âŠ¢ â˜…}
      â†’ Î³ â‰ˆâ‚‘ Ïƒ  â†’  c â‰ˆ N
        --------------------------
      â†’ (Î³ ,' c) â‰ˆâ‚‘ (ext-subst Ïƒ N)
â‰ˆâ‚‘-ext {Î“} {Î³} {Ïƒ} {c} {N} Î³â‰ˆâ‚‘Ïƒ câ‰ˆN {x} = goal
   where
   ext-cons : (Î³ ,' c) â‰ˆâ‚‘ (N â€¢ Ïƒ)
   ext-cons {Z} = câ‰ˆN
   ext-cons {S x} = Î³â‰ˆâ‚‘Ïƒ

   goal : (Î³ ,' c) x â‰ˆ ext-subst Ïƒ N x
   goal
       with ext-cons {x}
   ... | a rewrite sym (subst-zero-exts-cons{Î“}{âˆ…}{Ïƒ}{â˜…}{N}{â˜…}) = a
\end{code}

To prove `â‰ˆâ‚‘-ext`, we make use of the fact that `ext-subst Ïƒ N` is
equivalent to `N â€¢ Ïƒ` (by `subst-zero-exts-cons`). So we just
need to prove that `(Î³ ,' c) â‰ˆâ‚‘ (N â€¢ Ïƒ)`, which is easy.
We proceed by cases on the input variable.

* If it is `Z`, then we immediately conclude using the
  premise `c â‰ˆ N`.

* If it is `S x`, then we immediately conclude using
  premise `Î³ â‰ˆâ‚‘ Ïƒ`.


We arive at the main lemma: if `M` big steps to a
closure `c` in environment `Î³`, and if `Î³ â‰ˆâ‚‘ Ïƒ`, then `âŸª Ïƒ âŸ« M` reduces
to some term `N` that is equivalent to `c`. We describe the proof
below.

\begin{code}
â‡“â†’â€”â† Ã—ğ”¹ : âˆ€{Î“}{Î³ : ClosEnv Î“}{Ïƒ : Subst Î“ âˆ…}{M : Î“ âŠ¢ â˜…}{c : Clos}
       â†’ Î³ âŠ¢ M â‡“ c  â†’  Î³ â‰ˆâ‚‘ Ïƒ
         ---------------------------------------
       â†’ Î£[ N âˆˆ âˆ… âŠ¢ â˜… ] (âŸª Ïƒ âŸ« M â€”â†  N) Ã— c â‰ˆ N
â‡“â†’â€”â† Ã—ğ”¹ {Î³ = Î³} (â‡“-var{x = x} Î³xâ‰¡LÎ´ Î´âŠ¢Lâ‡“c) Î³â‰ˆâ‚‘Ïƒ
    with Î³ x | Î³â‰ˆâ‚‘Ïƒ {x} | Î³xâ‰¡LÎ´
... | clos L Î´ | âŸ¨ Ï„ , âŸ¨ Î´â‰ˆâ‚‘Ï„ , Ïƒxâ‰¡Ï„L âŸ© âŸ© | refl
    with â‡“â†’â€”â† Ã—ğ”¹{Ïƒ = Ï„} Î´âŠ¢Lâ‡“c Î´â‰ˆâ‚‘Ï„
... | âŸ¨ N , âŸ¨ Ï„Lâ€”â† N , câ‰ˆN âŸ© âŸ© rewrite Ïƒxâ‰¡Ï„L =
      âŸ¨ N , âŸ¨ Ï„Lâ€”â† N , câ‰ˆN âŸ© âŸ©
â‡“â†’â€”â† Ã—ğ”¹ {Ïƒ = Ïƒ} {c = clos (Æ› N) Î³} â‡“-lam Î³â‰ˆâ‚‘Ïƒ =
    âŸ¨ âŸª Ïƒ âŸ« (Æ› N) , âŸ¨ âŸª Ïƒ âŸ« (Æ› N) [] , âŸ¨ Ïƒ , âŸ¨ Î³â‰ˆâ‚‘Ïƒ , refl âŸ© âŸ© âŸ© âŸ©
â‡“â†’â€”â† Ã—ğ”¹{Î“}{Î³} {Ïƒ = Ïƒ} {L Â· M} {c} (â‡“-app {N = N} Lâ‡“Æ›NÎ´ Nâ‡“c) Î³â‰ˆâ‚‘Ïƒ
    with â‡“â†’â€”â† Ã—ğ”¹{Ïƒ = Ïƒ} Lâ‡“Æ›NÎ´ Î³â‰ˆâ‚‘Ïƒ
... | âŸ¨ _ , âŸ¨ ÏƒLâ€”â† Æ›Ï„N , âŸ¨ Ï„ , âŸ¨ Î´â‰ˆâ‚‘Ï„ , â‰¡Æ›Ï„N âŸ© âŸ© âŸ© âŸ© rewrite â‰¡Æ›Ï„N
    with â‡“â†’â€”â† Ã—ğ”¹ {Ïƒ = ext-subst Ï„ (âŸª Ïƒ âŸ« M)} Nâ‡“c
           (Î» {x} â†’ â‰ˆâ‚‘-ext{Ïƒ = Ï„} Î´â‰ˆâ‚‘Ï„ âŸ¨ Ïƒ , âŸ¨ Î³â‰ˆâ‚‘Ïƒ , refl âŸ© âŸ© {x})
       | Î²{âˆ…}{âŸª exts Ï„ âŸ« N}{âŸª Ïƒ âŸ« M}
... | âŸ¨ N' , âŸ¨ â€”â† N' , câ‰ˆN' âŸ© âŸ© | Æ›Ï„NÂ·ÏƒMâ€”â†’
    rewrite sub-sub{M = N}{Ïƒâ‚ = exts Ï„}{Ïƒâ‚‚ = subst-zero (âŸª Ïƒ âŸ« M)} =
    let rs = (Æ› âŸª exts Ï„ âŸ« N) Â· âŸª Ïƒ âŸ« M â€”â†’âŸ¨ Æ›Ï„NÂ·ÏƒMâ€”â†’ âŸ© â€”â† N' in
    let g = â€”â† -trans (appL-cong ÏƒLâ€”â† Æ›Ï„N) rs in
    âŸ¨ N' , âŸ¨ g , câ‰ˆN' âŸ© âŸ©
\end{code}

The proof is by induction on `Î³ âŠ¢ M â‡“ c`. We have three cases
to consider.

* Case `â‡“-var`.
  So we have `Î³ x â‰¡ clos L Î´` and `Î´ âŠ¢ L â‡“ c`.
  We need to show that `âŸª Ïƒ âŸ« x â€”â†  N` and `c â‰ˆ N` for some `N`.
  The premise `Î³ â‰ˆâ‚‘ Ïƒ` tells us that `Î³ x â‰ˆ Ïƒ x`, so `clos L Î´ â‰ˆ Ïƒ x`.
  By the definition of `â‰ˆ`, there exists a `Ï„` such that
  `Î´ â‰ˆâ‚‘ Ï„` and `Ïƒ x â‰¡ âŸª Ï„ âŸ« L `.
  Using `Î´ âŠ¢ L â‡“ c` and `Î´ â‰ˆâ‚‘ Ï„`, 
  the induction hypothesis gives us
  `âŸª Ï„ âŸ« L â€”â†  N` and `c â‰ˆ N` for some `N`.
  So we have shown that `âŸª Ïƒ âŸ« x â€”â†  N` and `c â‰ˆ N` for some `N`.

* Case `â‡“-lam`.
  We immediately have `âŸª Ïƒ âŸ« (Æ› N) â€”â†  âŸª Ïƒ âŸ« (Æ› N)`
  and `clos (âŸª Ïƒ âŸ« (Æ› N)) Î³ â‰ˆ âŸª Ïƒ âŸ« (Æ› N)`.

* Case `â‡“-app`.
  Using `Î³ âŠ¢ L â‡“ clos N Î´` and `Î³ â‰ˆâ‚‘ Ïƒ`, 
  the induction hypothesis gives us
  
        âŸª Ïƒ âŸ« L â€”â†  Æ› âŸª exts Ï„ âŸ« N                                           (1)
  
  and `Î´ â‰ˆâ‚‘ Ï„` for some `Ï„`.
  From `Î³â‰ˆâ‚‘Ïƒ` we have `clos M Î³ â‰ˆ âŸª Ïƒ âŸ« M`.
  Then with `(Î´ ,' clos M Î³) âŠ¢ N â‡“ c`,
  the induction hypothesis gives us `c â‰ˆ N'` and
  
        âŸª exts Ï„ â¨Ÿ subst-zero (âŸª Ïƒ âŸ« M) âŸ« N â€”â†  N'                           (2)
  
  Meanwhile, by `Î²`, we have

        (Æ› âŸª exts Ï„ âŸ« N) Â· âŸª Ïƒ âŸ« M â€”â†’ âŸª subst-zero (âŸª Ïƒ âŸ« M) âŸ« (âŸª exts Ï„ âŸ« N)

  which is the same as the following, by `sub-sub`.
  
        (Æ› âŸª exts Ï„ âŸ« N) Â· âŸª Ïƒ âŸ« M â€”â†’ âŸª exts Ï„ â¨Ÿ subst-zero (âŸª Ïƒ âŸ« M) âŸ«  N  (3)

  Using (3) and (2) we have
  
        (Æ› âŸª exts Ï„ âŸ« N) Â· âŸª Ïƒ âŸ« M â€”â†  N'                                    (4)

  From (1) we have

        âŸª Ïƒ âŸ« L Â· âŸª Ïƒ âŸ« M â€”â†  (Æ› âŸª exts Ï„ âŸ« N) Â· âŸª Ïƒ âŸ« M

  which we combine with (4) to conclude that

        âŸª Ïƒ âŸ« L Â· âŸª Ïƒ âŸ« M â€”â†  N'


With the main lemma complete, we establish the forward direction
of the equivalence between the big-step semantics and beta reduction.

\begin{code}
cbnâ†’reduce :  âˆ€{M : âˆ… âŠ¢ â˜…}{Î”}{Î´ : ClosEnv Î”}{Nâ€² : Î” , â˜… âŠ¢ â˜…}
     â†’ âˆ…' âŠ¢ M â‡“ clos (Æ› Nâ€²) Î´
       -----------------------------
     â†’ Î£[ N âˆˆ âˆ… , â˜… âŠ¢ â˜… ] (M â€”â†  Æ› N)
cbnâ†’reduce {M}{Î”}{Î´}{Nâ€²} Mâ‡“c
    with â‡“â†’â€”â† Ã—ğ”¹{Ïƒ = ids} Mâ‡“c â‰ˆâ‚‘-id
... | âŸ¨ N , âŸ¨ rs , âŸ¨ Ïƒ , âŸ¨ h , eq2 âŸ© âŸ© âŸ© âŸ©
    rewrite sub-id{M = M} | eq2 =
    âŸ¨ âŸª exts Ïƒ âŸ« Nâ€² , rs âŸ©
\end{code}

The proof of the backward direction, that beta reduction to a lambda
implies that the big-step semantics produces a result, will leverage the
denotational semantics defined in the next chapter, and appears in the
chapter on Adequacy.

## Notes

In the seminal article _Call-by-name, call-by-value, and the
Î»-calculus_, Plotkin defined a call-by-name partial function similar
to the big-step semantics in this chapter, except that it used
substitution instead of environments. Corollary 2 (page 146) of his
article states that a term `M` beta reduces to a lambda abstraction if
and only if call-by-name produces a value. His proof is quite
different from ours in that it relies on two auxilliary reduction
relations called _left reduction_ and _standard reduction_. Theorem 1
(Standardisation) states that `M â€”â†  L` if and only if `M` goes to `L`
via standard reductions.  Corollary 1 then establishes that `M â€”â†  Æ› N`
if and only if `M` goes to `Æ› Nâ€²`, for some `Nâ€²`, by left
reduction. Theorem 2 then connects call-by-name evaluation to left
reduction.  Finally, Corollary 2 combines these results to show that
`M â€”â†  Æ› N` if and only if call-by-name produces a value.


## Unicode

This chapter uses the following unicode:

    â‰ˆ  U+2248  ALMOST EQUAL TO (\~~ or \approx)
    â‚‘  U+2091  LATIN SUBSCRIPT SMALL LETTER E (\_e)
    âŠ¢  U+22A2  RIGHT TACK (\|- or \vdash)
    â‡“  U+21DB  DOWNWARDS DOUBLE ARROW (\d= or \Downarrow)
    
