---
title     : "Lambda: Introduction to Lambda Calculus"
permalink : /Lambda/
---

*Add a couple of simpler examples* ``id Â· `zero`` and ``twoá¶œ Â· sucá¶œ Â· `zero``.

*Experiment with defining variables* smart constructor `` Æ›`_â‡’_ ``

\begin{code}
module plta.Lambda where
\end{code}

[This chapter was originally based on Chapter _Stlc_
of _Software Foundations_ (_Programming Language Foundations_).
It has now been updated, but if you spot any plagiarism
please let me know. â€“ P]

The _lambda-calculus_, first published by the logician Alonzo Church in
1932, is a core calculus with only three syntactic constructs:
variables, abstraction, and application.  It captures the key concept of
_functional abstraction_, which appears in pretty much every programming
language, in the form of either functions, procedures, or methods.
The _simply-typed lambda calculus_ (or STLC) is a variant of the
lambda calculus published by Church in 1940.  It has the three
constructs above for function types, plus whatever else is required
for base types. Church had a minimal base type with no operations.
We will instead echo Plotkin's _Programmable Computable
Functions_ (PCF), and add operations on natural numbers and
recursive function definitions.

This chapter formalises the simply-typed lambda calculus, giving its
syntax, small-step semantics, and typing rules.  The next chapter
[Properties]({{ site.baseurl }}{% link out/plta/Properties.md %})
proves its main properties, including
progress and preservation.  Following chapters will look at a number
of variants of lambda calculus.

Be aware that the approach we take here is _not_ our recommended
approach to formalisation.  Using de Bruijn indices and
inherently-typed terms, as we will do in
Chapter [DeBruijn]({{ site.baseurl }}{% link out/plta/DeBruijn.md %}),
leads to a more compact formulation.  Nonetheless, we begin with named
variables, partly because such terms are easier to read and partly
because the development is more traditional.

The development in this chapter was inspired by the corresponding
development in Chapter _Stlc_ of _Software Foundations_
(_Programming Language Foundations_).  We differ by
representing contexts explicitly (as lists pairing identifiers with
types) rather than as partial maps (which take identifiers to types),
which will corresponds better to our subsequent development of DeBruin
notation. We also differ by taking natural numbers as the base type
rather than booleans, allowing more sophisticated examples. In
particular, we will be able to show (twice!) that two plus two is
four.

## Imports

\begin{code}
open import Relation.Binary.PropositionalEquality using (_â‰¡_; _â‰¢_; refl)
open import Data.String using (String; _â‰Ÿ_)
open import Data.Nat using (â„•; zero; suc)
open import Data.Empty using (âŠ¥; âŠ¥-elim)
open import Relation.Nullary using (Dec; yes; no; Â¬_)
open import Relation.Nullary.Negation using (Â¬?)
\end{code}

## Syntax of terms

Terms have seven constructs. Three are for the core lambda calculus:

  * Variables `` x`
  * Abstractions `Æ› x â‡’ N`
  * Applications `L Â· M`

Three are for the naturals:

  * Zero `` `zero ``
  * Successor `` `suc ``
  * Case `` `case L [zeroâ‡’ M |suc x â‡’ N ] ``

And one is for recursion:

  * Fixpoint `Î¼ x â‡’ M`

Abstraction is also called _lambda abstraction_, and is the construct
from which the calculus takes its name.

With the exception of variables and fixpoints, each term
form either constructs a value of a given type (abstractions yield functions,
zero and successor yield booleans) or deconstructs it (applications use functions,
case terms use naturals). We will see this again when we come
to the rules for assigning types to terms, where constructors
correspond to introduction rules and deconstructors to eliminators.

Here is the syntax of terms in BNF.

    L, M, N  ::=
      ` x  |  Æ› x â‡’ N  |  L Â· M  |
      `zero  |  `suc M  |  `case L [zeroâ‡’ M |suc x â‡’ N]  |
      Î¼ x â‡’ M

And here it is formalised in Agda.
\begin{code}
Id : Set
Id = String

infix  5  Æ›_â‡’_
infix  5  Î¼_â‡’_
infixl 7  _Â·_
infix  8  `suc_
infix  9  `_

data Term : Set where
  `_                       :  Id â†’ Term
  Æ›_â‡’_                     :  Id â†’ Term â†’ Term
  _Â·_                      :  Term â†’ Term â†’ Term
  `zero                    :  Term
  `suc_                    :  Term â†’ Term
  `case_[zeroâ‡’_|suc_â‡’_]    :  Term â†’ Term â†’ Id â†’ Term â†’ Term
  Î¼_â‡’_                     :  Id â†’ Term â†’ Term
\end{code}
We represent identifiers by strings.  We choose precedence so that
lambda abstraction and fixpoint bind least tightly, then application,
then successor, and tightest of all is the constructor for variables.
Case expressions are self-bracketing.


### Example terms

Here are some example terms: the natural number two,
a function that adds naturals,
and a term that computes two plus two.
\begin{code}
two : Term
two = `suc `suc `zero

plus : Term
plus =  Î¼ "+" â‡’ Æ› "m" â‡’ Æ› "n" â‡’
         `case ` "m"
           [zeroâ‡’ ` "n"
           |suc "m" â‡’ `suc (` "+" Â· ` "m" Â· ` "n") ]

2+2 : Term
2+2 = plus Â· two Â· two
\end{code}
The recursive definition of addition is similar to our original
definition of `_+_` for naturals, as given in
Chapter [Naturals]({{ site.baseurl }}{% link out/plta/Naturals.md %}#plus).
Here variable "m" is bound twice, once in a lambda abstraction and once in
the successor branch of the case; the first use of "m" refers to
the former and the second to the latter.  Any use of "m" in the successor branch
must refer to the latter binding, and so we say that the latter binding _shadows_
the former.  Later we will confirm that two plus two is four, in other words that
the term

    plus Â· two Â· two

reduces to `` `suc `suc `suc `suc `zero ``.

As a second example, we use higher-order functions to represent
natural numbers.  In particular, the number _n_ is represented by a
function that accepts two arguments and applies the first _n_ times to the
second.  This is called the _Church representation_ of the
naturals.  Here are some example terms: the Church numeral two, a
function that adds Church numerals, a function to compute successor,
and a term that computes two plus two.
\begin{code}
twoá¶œ : Term
twoá¶œ =  Æ› "s" â‡’ Æ› "z" â‡’ ` "s" Â· (` "s" Â· ` "z")

plusá¶œ : Term
plusá¶œ =  Æ› "m" â‡’ Æ› "n" â‡’ Æ› "s" â‡’ Æ› "z" â‡’
         ` "m" Â· ` "s" Â· (` "n" Â· ` "s" Â· ` "z")

sucá¶œ : Term
sucá¶œ = Æ› "n" â‡’ `suc (` "n")

fourá¶œ : Term
fourá¶œ = plusá¶œ Â· twoá¶œ Â· twoá¶œ Â· sucá¶œ Â· `zero
\end{code}
The Church numeral for two takes two arguments `s` and `z`
and applies `s` twice to `z`.
Addition takes two numerals `m` and `n`, a
function `s` and an argument `z`, and it uses `m` to apply `s` to the
result of using `n` to apply `s` to `z`; hence `s` is applied `m` plus
`n` times to `z`, yielding the Church numeral for the sum of `m` and
`n`.  For convenience, we define a function that computes successor;
to convert a Church numeral to the corresponding natural, we apply
it to this function and the natural number zero.
Again, later we will confirm that two plus two is four,
in other words that the term

    plusá¶œ Â· twoá¶œ Â· twoá¶œ Â· sucá¶œ Â· `zero

reduces to `` `suc `suc `suc `suc `zero ``.


#### Exercise (`mul`)

Write out the defintion of a lambda term that multiplies
two natural numbers.


### Formal vs informal

In informal presentation of formal semantics, one uses choice of
variable name to disambiguate and writes `x` rather than `` x`
for a term that is a variable. Agda requires we distinguish.

Similarly, informal presentation often use the same notation for
function types, lambda abstraction, and function application in both
the object language (the language one is describing) and the
meta-language (the language in which the description is written),
trusting readers can use context to distinguish the two.  Agda is is
not quite so forgiving, so here we use `Æ› x â‡’ N` and `L Â· M` for the
object language, as compared to `Î» x â†’ N` and `L M` in our
meta-language, Agda.


### Bound and free variables

In an abstraction `Æ› x â‡’ N` we call `x` the _bound_ variable
and `N` the _body_ of the abstraction.  A central feature
of lambda calculus is that consistent renaming of bound variables
leaves the meaning of a term unchanged.  Thus the five terms

* `` Æ› "s" â‡’ Æ› "z" â‡’ ` "s" Â· (` "s" Â· ` "z") ``
* `` Æ› "f" â‡’ Æ› "x" â‡’ ` "f" Â· (` "f" Â· ` "x") ``
* `` Æ› "sam" â‡’ Æ› "zelda" â‡’ ` "sam" Â· (` "sam" Â· ` "zelda") ``
* `` Æ› "z" â‡’ Æ› "s" â‡’ ` "z" Â· (` "z" Â· ` "s") ``
* `` Æ› "ðŸ˜‡" â‡’ Æ› "ðŸ˜ˆ" â‡’ # "ðŸ˜‡" Â· (# "ðŸ˜‡" Â· # "ðŸ˜ˆ" ) ``

are all considered equivalent.  Following the convention introduced
by Haskell Curry, who used the Greek letter `Î±` (_alpha_) to label such rules,
this equivalence relation is called _alpha renaming_.

As we descend from a term into its subterms, variables
that are bound may become free.  Consider the following terms.

* `` Æ› "s" â‡’ Æ› "z" â‡’ ` "s" Â· (` "s" Â· ` "z") ``
  has both `s` and `z` as bound variables.

* `` Æ› "z" â‡’ ` "s" Â· (` "s" Â· ` "z") ``
  has `s` bound and `z` free.

* `` ` "s" Â· (` "s" Â· ` "z") ``
  has both `s` and `z` as free variables.

We say that a term with no free variables is _closed_; otherwise it is
_open_.  Of the three terms above, the first is closed and the other
two are open.  We will focus on reduction of closed terms.

Different occurrences of a variable may be bound and free.
In the term

    (Æ› "x" â‡’ ` "x") Â· ` "x"

the inner occurrence of `x` is bound while the outer occurrence is free.
By alpha renaming, the term above is equivalent to

    (Æ› "y" â‡’ ` "y") Â· ` "x"

in which `y` is bound and `x` is free.  A common convention, called the
_Barendregt convention_, is to use alpha renaming to ensure that the bound
variables in a term are distinct from the free variables, which can
avoid confusions that may arise if bound and free variables have the
same names.

Case and recursion also introduce bound variables, which are also subject
to alpha renaming. In the term

    Î¼ "+" â‡’ Æ› "m" â‡’ Æ› "n" â‡’
      `case ` "m"
        [zeroâ‡’ ` "n"
        |suc "m" â‡’ `suc (` "+" Â· ` "m" Â· ` "n") ]

notice that there are two binding occurrences of `m`, one in the first
line and one in the last line.  It is equivalent to the following term,

    Î¼ "plus" â‡’ Æ› "x" â‡’ Æ› "y" â‡’
      `case ` "x"
        [zeroâ‡’ ` "y"
        |suc "xâ€²" â‡’ `suc (` "plus" Â· ` "xâ€²" Â· ` "y") ]

where the two binding occurrences corresponding to `m` now have distinct
names, `x` and `xâ€²`.


## Values

A _value_ is a term that corresponds to an answer.
Thus, `` `suc `suc `suc `suc `zero` `` is a value,
while `` plus Â· two Â· two `` is not.
Following convention, we treat all function abstractions
as values; thus, `` plus `` by itself is considered a value.

The predicate `Value M` holds if term `M` is a value.

\begin{code}
data Value : Term â†’ Set where

  V-Æ› : âˆ€ {x N}
      ---------------
    â†’ Value (Æ› x â‡’ N)

  V-zero :
      -----------
      Value `zero

  V-suc : âˆ€ {V}
    â†’ Value V
      --------------
    â†’ Value (`suc V)
\end{code}

We let `V` and `W` range over values.


### Formal vs informal

In informal presentations of formal semantics, using
`V` as the name of a metavariable is sufficient to
indicate that it is a value. In Agda, we must explicitly
invoke the `Value` predicate.

### Other approaches

An alternative is not to focus on closed terms,
to treat variables as values, and to treat
`Æ› x â‡’ N` as a value only if `N` is a value.
Indeed, this is how Agda normalises terms.
We consider this approach in
Chapter [Untyped]({{ site.baseurl }}{% link out/plta/Untyped.md %}).


## Substitution

The heart of lambda calculus is the operation of
substituting one term for a variable in another term.
Substitution plays a key role in defining the
operational semantics of function application.
For instance, we have

      (Æ› "s" â‡’ Æ› "z" â‡’ ` "s" Â· (` "s" Â· ` "z")) Â· sucá¶œ Â· `zero
    â€”â†’
      (Æ› "z" â‡’ sucá¶œ Â· (sucá¶œ Â· "z")) Â· `zero
    â€”â†’
      sucá¶œ Â· (sucá¶œ Â· `zero)

where we substitute `sucá¶œ` for `` ` "s" `` and `` `zero `` for `` ` "z" ``
in the body of the function abstraction.

We write substitution as `N [ x := V ]`, meaning
"substitute term `V` for free occurrences of variable `x` in term `N`",
or, more compactly, "substitute `V` for `x` in `N`".
Substitution works if `V` is any closed term;
it need not be a value, but we use `V` since in fact we
usually substitute values.

Here are some examples:

* `` (sucá¶œ Â· (sucá¶œ Â· ` "z")) [ "z" := `zero ] `` yields
  `` sucá¶œ Â· (sucá¶œ Â· `zero) ``
* `` (Æ› "z" â‡’ ` "s" Â· (` "s" Â· ` "z")) [ "s" := sucá¶œ ] `` yields
     Æ› "z" â‡’ sucá¶œ Â· (sucá¶œ Â· ` "z") ``
* `` (Æ› "x" â‡’ ` "y") [ "y" := `zero ] `` yields `` Æ› "x" â‡’ `zero ``
* `` (Æ› "x" â‡’ ` "x") [ "x" := `zero ] `` yields `` Æ› "x" â‡’ ` "x" ``
* `` (Æ› "y" â‡’ ` "y") [ "x" := `zero ] `` yields `` Æ› "x" â‡’ ` "x" ``

In the last but one example, substituting `` `zero `` for `x` in
`` Æ› "x" â‡’ ` "x" `` does _not_ yield `` Æ› "x" â‡’ `zero ``,
since `x` is bound in the lambda abstraction.
The choice of bound names is irrelevant: both
`` Æ› "x" â‡’ ` "x" `` and `` Æ› "y" â‡’ ` "y" `` stand for the
identity function.  One way to think of this is that `x` within
the body of the abstraction stands for a _different_ variable than
`x` outside the abstraction, they just happen to have the same name.

We will give a definition of substitution that is only valid
when term substituted for the variable is closed. This is because
substitution by terms that are _not_ closed may require renaming
of bound variables. For example:

* `` (Æ› "x" â‡’ ` "x" Â· ` "y") [ "y" := ` "x" Â· `zero] `` should not yield
  `` (Æ› "x" â‡’ ` "x" Â· (` "x" Â· ` `zero)) ``

Instead, we should rename the bound variable to avoid capture.

* `` (Æ› "x" â‡’ ` "x" Â· ` "y") [ "y" := ` "x" Â· `zero ] `` should yield
  `` Æ› "z" â‡’ ` "z" Â· (` "x" Â· `zero) ``

Formal definition of substitution with suitable renaming is considerably
more complex, so we avoid it by restricting to substitution by closed terms,
which will be adequate for our purposes.

Here is the formal definition of substitution by closed terms in Agda.

\begin{code}
infix 9 _[_:=_]
infix 9 _âŸ¨_âŸ©[_:=_]

_[_:=_]   : Term â†’ Id â†’ Term â†’ Term

_âŸ¨_âŸ©[_:=_] : Term â†’ Id â†’ Id â†’ Term â†’ Term
N âŸ¨ x âŸ©[ y := V ] with x â‰Ÿ y
... | yes _                  =  N
... | no  _                  =  N [ y := V ]

(` x) [ y := V ] with x â‰Ÿ y
... | yes _                  =  V
... | no  _                  =  ` x
(Æ› x â‡’ N) [ y := V ]         =  Æ› x â‡’ N âŸ¨ x âŸ©[ y := V ]
(L Â· M) [ y := V ]           =  (L [ y := V ]) Â· (M [ y := V ])
(`zero) [ y := V ]           =  `zero
(`suc M) [ y := V ]          =  `suc (M [ y := V ])
(`case L
  [zeroâ‡’ M
  |suc x â‡’ N ]) [ y := V ]   =  `case L [ y := V ]
                                   [zeroâ‡’ M [ y := V ]
                                   |suc x â‡’ N âŸ¨ x âŸ©[ y := V ] ]
(Î¼ x â‡’ N) [ y := V ]         =  Î¼ x â‡’ (N âŸ¨ x âŸ©[ y := V ])

{-
infix 9 _[_:=_]

_[_:=_] : Term â†’ Id â†’ Term â†’ Term
(` x) [ y := V ] with x â‰Ÿ y
... | yes _                      =  V
... | no  _                      =  ` x
(Æ› x â‡’ N) [ y := V ] with x â‰Ÿ y
... | yes _                      =  Æ› x â‡’ N
... | no  _                      =  Æ› x â‡’ (N [ y := V ])
(L Â· M) [ y := V ]               =  (L [ y := V ]) Â· (M [ y := V ])
(`zero) [ y := V ]               =  `zero
(`suc M) [ y := V ]              =  `suc (M [ y := V ])
(`case L
  [zeroâ‡’ M
  |suc x â‡’ N ])
  [ y := V ] with x â‰Ÿ y
... | yes _                      =  `case L [ y := V ]
                                       [zeroâ‡’ M [ y := V ]
                                       |suc x â‡’ N ]
... | no  _                      =  `case L [ y := V ]
                                       [zeroâ‡’ M [ y := V ]
                                       |suc x â‡’ N [ y := V ] ]
(Î¼ x â‡’ N) [ y := V ] with x â‰Ÿ y
... | yes _                      =  Î¼ x â‡’ N
... | no  _                      =  Î¼ x â‡’ (N [ y := V ])
-}
\end{code}

Let's unpack the first three cases.

* For variables, we compare `y`, the substituted variable,
with `x`, the variable in the term. If they are the same,
we yield `V`, otherwise we yield `x` unchanged.

* For abstractions, we compare `w`, the variable we are substituting for,
with `x`, the variable bound in the abstraction. If they are the same,
we yield the abstraction unchanged, otherwise we substitute inside the body.

* For application, we recursively substitute in the function
and the argument.

Case expressions and recursion also have bound variables that are
treated similarly to those in lambda abstractions.  Otherwise we
simply push substitution recursively into the subterms.


### Examples

Here is confirmation that the examples above are correct.

\begin{code}
_ : (sucá¶œ Â· (sucá¶œ Â· ` "z")) [ "z" := `zero ] â‰¡  sucá¶œ Â· (sucá¶œ Â· `zero)
_ = refl

_ : (Æ› "z" â‡’ ` "s" Â· (` "s" Â· ` "z")) [ "s" := sucá¶œ ] â‰¡  Æ› "z" â‡’ sucá¶œ Â· (sucá¶œ Â· ` "z")
_ = refl

_ : (Æ› "x" â‡’ ` "y") [ "y" := `zero ] â‰¡ Æ› "x" â‡’ `zero
_ = refl

_ : (Æ› "x" â‡’ ` "x") [ "x" := `zero ] â‰¡ Æ› "x" â‡’ ` "x"
_ = refl

_ : (Æ› "y" â‡’ ` "y") [ "x" := `zero ] â‰¡ Æ› "y" â‡’ ` "y"
_ = refl
\end{code}

#### Quiz

What is the result of the following substitution?

    (Æ› "y" â‡’ ` "x" Â· (Æ› "x" â‡’ ` "x")) [ "x" := `zero ]

1. `` (Æ› "y" â‡’ ` "x" Â· (Æ› "x" â‡’ ` "x")) ``
2. `` (Æ› "y" â‡’ ` "x" Â· (Æ› "x" â‡’ `zero)) ``
3. `` (Æ› "y" â‡’ `zero Â· (Æ› "x" â‡’ ` "x")) ``
4. `` (Æ› "y" â‡’ `zero Â· (Æ› "x" â‡’ `zero)) ``


## Reduction

We give the reduction rules for call-by-value lambda calculus.  To
reduce an application, first we reduce the left-hand side until it
becomes a value (which must be an abstraction); then we reduce the
right-hand side until it becomes a value; and finally we substitute
the argument for the variable in the abstraction.

In an informal presentation of the operational semantics,
the rules for reduction of applications are written as follows.

    L â€”â†’ Lâ€²
    -------------- Î¾-Â·â‚
    L Â· M â€”â†’ Lâ€² Â· M

    M â€”â†’ Mâ€²
    -------------- Î¾-Â·â‚‚
    V Â· M â€”â†’ V Â· Mâ€²

    ---------------------------- Î²-Æ›
    (Æ› x â‡’ N) Â· V â€”â†’ N [ x := V ]

The Agda version of the rules below will be similar, except that universal
quantifications are made explicit, and so are the predicates that indicate
which terms are values.

The rules break into two sorts. Compatibility rules direct us to
reduce some part of a term.  We give them names starting with the
Greek letter `Î¾` (_xi_).  Once a term is sufficiently reduced, it will
consist of a constructor and a deconstructor, in our case `Î»` and `Â·`,
which reduces directly.  We give them names starting with the Greek
letter `Î²` (_beta_) and such rules are traditionally called _beta rules_.

The rules are deterministic, in that at most one rule applies to every
term.  We will show in the next chapter that for every well-typed term
either a reduction applies or it is a value.

For numbers, zero does not reduce and successor reduces the subterm.
A case expression reduces its argument to a number, and then chooses
the zero or successor branch as appropriate.  A fixpoint replaces
the bound variable by the entire fixpoint term.

Here are the rules formalised in Agda.

\begin{code}
infix 4 _â€”â†’_

data _â€”â†’_ : Term â†’ Term â†’ Set where

  Î¾-Â·â‚ : âˆ€ {L Lâ€² M}
    â†’ L â€”â†’ Lâ€²
      -----------------
    â†’ L Â· M â€”â†’ Lâ€² Â· M

  Î¾-Â·â‚‚ : âˆ€ {V M Mâ€²}
    â†’ Value V
    â†’ M â€”â†’ Mâ€²
      -----------------
    â†’ V Â· M â€”â†’ V Â· Mâ€²

  Î²-Æ› : âˆ€ {x N V}
    â†’ Value V
      ------------------------------
    â†’ (Æ› x â‡’ N) Â· V â€”â†’ N [ x := V ]

  Î¾-suc : âˆ€ {M Mâ€²}
    â†’ M â€”â†’ Mâ€²
      ------------------
    â†’ `suc M â€”â†’ `suc Mâ€²

  Î¾-case : âˆ€ {x L Lâ€² M N}
    â†’ L â€”â†’ Lâ€²
      -----------------------------------------------------------------
    â†’ `case L [zeroâ‡’ M |suc x â‡’ N ] â€”â†’ `case Lâ€² [zeroâ‡’ M |suc x â‡’ N ]

  Î²-zero : âˆ€ {x M N}
      ----------------------------------------
    â†’ `case `zero [zeroâ‡’ M |suc x â‡’ N ] â€”â†’ M

  Î²-suc : âˆ€ {x V M N}
    â†’ Value V
      ---------------------------------------------------
    â†’ `case `suc V [zeroâ‡’ M |suc x â‡’ N ] â€”â†’ N [ x := V ]

  Î²-Î¼ : âˆ€ {x M}
      ------------------------------
    â†’ Î¼ x â‡’ M â€”â†’ M [ x := Î¼ x â‡’ M ]
\end{code}

The reduction rules are carefully designed to ensure that subterms
of a term are reduced to values before the whole term is reduced.
This is referred to as _call by value_ reduction.

Further, we have arranged that subterms are reduced in a
left-to-right order.  This means that reduction is _deterministic_:
for any term, there is at most one other term to which it reduces.
Put another way, our reduction relation `â€”â†’` is in fact a function.


#### Quiz

What does the following term step to?

    (Æ› "x" â‡’ ` "x") Â· (Æ› "x" â‡’ ` "x")  â€”â†’  ???

1.  `` (Æ› "x" â‡’ ` "x") ``
2.  `` (Æ› "x" â‡’ ` "x") Â· (Æ› "x" â‡’ ` "x") ``
3.  `` (Æ› "x" â‡’ ` "x") Â· (Æ› "x" â‡’ ` "x") Â· (Æ› "x" â‡’ ` "x") ``

What does the following term step to?

    (Æ› "x" â‡’ ` "x") Â· (Æ› "x" â‡’ ` "x") Â· (Æ› "x" â‡’ ` "x")  â€”â†’  ???

1.  `` (Æ› "x" â‡’ ` "x") ``
2.  `` (Æ› "x" â‡’ ` "x") Â· (Æ› "x" â‡’ ` "x") ``
3.  `` (Æ› "x" â‡’ ` "x") Â· (Æ› "x" â‡’ ` "x") Â· (Æ› "x" â‡’ ` "x") ``

What does the following term step to?  (Where `two` and `sucá¶œ` are as defined above.)

    two Â· sucá¶œ Â· `zero  â€”â†’  ???

1.  `` sucá¶œ Â· (sucá¶œ Â· `zero) ``
2.  `` (Æ› "z" â‡’ sucá¶œ Â· (sucá¶œ Â· ` "z")) Â· `zero ``
3.  `` `zero ``


## Reflexive and transitive closure

A single step is only part of the story. In general, we wish to repeatedly
step a closed term until it reduces to a value.  We do this by defining
the reflexive and transitive closure `â€”â† ` of the step relation `â€”â†’`.

We define reflexive and transitive closure as a sequence of zero or
more steps of the underlying relation, along lines similar to that for
reasoning about chains of equalities
Chapter [Equality]({{ site.baseurl }}{% link out/plta/Equality.md %}).
\begin{code}
infix  2 _â€”â† _
infix  1 begin_
infixr 2 _â€”â†’âŸ¨_âŸ©_
infix  3 _âˆŽ

data _â€”â† _ : Term â†’ Term â†’ Set where
  _âˆŽ : âˆ€ M
      ---------
    â†’ M â€”â†  M

  _â€”â†’âŸ¨_âŸ©_ : âˆ€ L {M N}
    â†’ L â€”â†’ M
    â†’ M â€”â†  N
      ---------
    â†’ L â€”â†  N

begin_ : âˆ€ {M N} â†’ (M â€”â†  N) â†’ (M â€”â†  N)
begin Mâ€”â† N = Mâ€”â† N
\end{code}
We can read this as follows.

* From term `M`, we can take no steps, giving a step of type `M â€”â†  M`.
  It is written `M âˆŽ`.

* From term `L` we can take a single of type `L â€”â†’ M` followed by zero
  or more steps of type `M â€”â†  N`, giving a step of type `L â€”â†  N`. It is
  written `L â€”â†’âŸ¨ Lâ€”â†’M âŸ© Mâ€”â† N`, where `Lâ€”â†’M` and `Mâ€”â† N` are steps of the
  appropriate type.

The notation is chosen to allow us to lay out example reductions in an
appealing way, as we will see in the next section.

As alternative is to define reflexive and transitive closure directly,
as the smallest relation that includes `â€”â†’` and is also reflexive
and transitive.  We could do so as follows.
\begin{code}
data _â€”â† â€²_ : Term â†’ Term â†’ Set where

  step : âˆ€ {M N}
    â†’ M â€”â†’ N
      ------
    â†’ M â€”â† â€² N

  refl : âˆ€ {M}
      ------
    â†’ M â€”â† â€² M

  trans : âˆ€ {L M N}
    â†’ L â€”â† â€² M
    â†’ M â€”â† â€² N
      ------
    â†’ L â€”â† â€² N
\end{code}
The three constructors specify, respectively, that `â€”â† ` includes `â€”â†’`
and is reflexive and transitive.

It is a straightforward exercise to show the two are equivalent.

#### Exercise (`â€”â† â‰ƒâ€”â† â€²`)

Show that the two notions of reflexive and transitive closure
above are isomorphic.


## Examples

We start with a simple example. The Church numeral two applied to the
successor function and zero yields the natural number two.
\begin{code}
_ : twoá¶œ Â· sucá¶œ Â· `zero â€”â†  `suc `suc `zero
_ =
  begin
    twoá¶œ Â· sucá¶œ Â· `zero
  â€”â†’âŸ¨ Î¾-Â·â‚ (Î²-Æ› V-Æ›) âŸ©
    (Æ› "z" â‡’ sucá¶œ Â· (sucá¶œ Â· ` "z")) Â· `zero
  â€”â†’âŸ¨ Î²-Æ› V-zero âŸ©
    sucá¶œ Â· (sucá¶œ Â· `zero)
  â€”â†’âŸ¨ Î¾-Â·â‚‚ V-Æ› (Î²-Æ› V-zero) âŸ©
    sucá¶œ Â· `suc `zero
  â€”â†’âŸ¨ Î²-Æ› (V-suc V-zero) âŸ©
   `suc (`suc `zero)
  âˆŽ
\end{code}

Here is a sample reduction demonstrating that two plus two is four.
\begin{code}
_ : plus Â· two Â· two â€”â†  `suc `suc `suc `suc `zero
_ =
  begin
    plus Â· two Â· two
  â€”â†’âŸ¨ Î¾-Â·â‚ (Î¾-Â·â‚ Î²-Î¼) âŸ©
    (Æ› "m" â‡’ Æ› "n" â‡’
      `case ` "m" [zeroâ‡’ ` "n" |suc "m" â‡’ `suc (plus Â· ` "m" Â· ` "n") ])
        Â· two Â· two
  â€”â†’âŸ¨ Î¾-Â·â‚ (Î²-Æ› (V-suc (V-suc V-zero))) âŸ©
    (Æ› "n" â‡’
      `case two [zeroâ‡’ ` "n" |suc "m" â‡’ `suc (plus Â· ` "m" Â· ` "n") ])
         Â· two
  â€”â†’âŸ¨ Î²-Æ› (V-suc (V-suc V-zero)) âŸ©
    `case two [zeroâ‡’ two |suc "m" â‡’ `suc (plus Â· ` "m" Â· two) ]
  â€”â†’âŸ¨ Î²-suc (V-suc V-zero) âŸ©
    `suc (plus Â· `suc `zero Â· two)
  â€”â†’âŸ¨ Î¾-suc (Î¾-Â·â‚ (Î¾-Â·â‚ Î²-Î¼)) âŸ©
    `suc ((Æ› "m" â‡’ Æ› "n" â‡’
      `case ` "m" [zeroâ‡’ ` "n" |suc "m" â‡’ `suc (plus Â· ` "m" Â· ` "n") ])
        Â· `suc `zero Â· two)
  â€”â†’âŸ¨ Î¾-suc (Î¾-Â·â‚ (Î²-Æ› (V-suc V-zero))) âŸ©
    `suc ((Æ› "n" â‡’
      `case `suc `zero [zeroâ‡’ ` "n" |suc "m" â‡’ `suc (plus Â· ` "m" Â· ` "n") ])
        Â· two)
  â€”â†’âŸ¨ Î¾-suc (Î²-Æ› (V-suc (V-suc V-zero))) âŸ©
    `suc (`case `suc `zero [zeroâ‡’ two |suc "m" â‡’ `suc (plus Â· ` "m" Â· two) ])
  â€”â†’âŸ¨ Î¾-suc (Î²-suc V-zero) âŸ©
    `suc `suc (plus Â· `zero Â· two)
  â€”â†’âŸ¨ Î¾-suc (Î¾-suc (Î¾-Â·â‚ (Î¾-Â·â‚ Î²-Î¼))) âŸ©
    `suc `suc ((Æ› "m" â‡’ Æ› "n" â‡’
      `case ` "m" [zeroâ‡’ ` "n" |suc "m" â‡’ `suc (plus Â· ` "m" Â· ` "n") ])
        Â· `zero Â· two)
  â€”â†’âŸ¨ Î¾-suc (Î¾-suc (Î¾-Â·â‚ (Î²-Æ› V-zero))) âŸ©
    `suc `suc ((Æ› "n" â‡’
      `case `zero [zeroâ‡’ ` "n" |suc "m" â‡’ `suc (plus Â· ` "m" Â· ` "n") ])
        Â· two)
  â€”â†’âŸ¨ Î¾-suc (Î¾-suc (Î²-Æ› (V-suc (V-suc V-zero)))) âŸ©
    `suc `suc (`case `zero [zeroâ‡’ two |suc "m" â‡’ `suc (plus Â· ` "m" Â· two) ])
  â€”â†’âŸ¨ Î¾-suc (Î¾-suc Î²-zero) âŸ©
    `suc (`suc (`suc (`suc `zero)))
  âˆŽ
\end{code}

And here is a similar sample reduction for Church numerals.
\begin{code}
_ : fourá¶œ â€”â†  `suc `suc `suc `suc `zero
_ =
  begin
    (Æ› "m" â‡’ Æ› "n" â‡’ Æ› "s" â‡’ Æ› "z" â‡’ ` "m" Â· ` "s" Â· (` "n" Â· ` "s" Â· ` "z"))
      Â· twoá¶œ Â· twoá¶œ Â· sucá¶œ Â· `zero
  â€”â†’âŸ¨ Î¾-Â·â‚ (Î¾-Â·â‚ (Î¾-Â·â‚ (Î²-Æ› V-Æ›))) âŸ©
    (Æ› "n" â‡’ Æ› "s" â‡’ Æ› "z" â‡’ twoá¶œ Â· ` "s" Â· (` "n" Â· ` "s" Â· ` "z"))
      Â· twoá¶œ Â· sucá¶œ Â· `zero
  â€”â†’âŸ¨ Î¾-Â·â‚ (Î¾-Â·â‚ (Î²-Æ› V-Æ›)) âŸ©
    (Æ› "s" â‡’ Æ› "z" â‡’ twoá¶œ Â· ` "s" Â· (twoá¶œ Â· ` "s" Â· ` "z")) Â· sucá¶œ Â· `zero
  â€”â†’âŸ¨ Î¾-Â·â‚ (Î²-Æ› V-Æ›) âŸ©
    (Æ› "z" â‡’ twoá¶œ Â· sucá¶œ Â· (twoá¶œ Â· sucá¶œ Â· ` "z")) Â· `zero
  â€”â†’âŸ¨ Î²-Æ› V-zero âŸ©
    twoá¶œ Â· sucá¶œ Â· (twoá¶œ Â· sucá¶œ Â· `zero)
  â€”â†’âŸ¨ Î¾-Â·â‚ (Î²-Æ› V-Æ›) âŸ©
    (Æ› "z" â‡’ sucá¶œ Â· (sucá¶œ Â· ` "z")) Â· (twoá¶œ Â· sucá¶œ Â· `zero)
  â€”â†’âŸ¨ Î¾-Â·â‚‚ V-Æ› (Î¾-Â·â‚ (Î²-Æ› V-Æ›)) âŸ©
    (Æ› "z" â‡’ sucá¶œ Â· (sucá¶œ Â· ` "z")) Â· ((Æ› "z" â‡’ sucá¶œ Â· (sucá¶œ Â· ` "z")) Â· `zero)
  â€”â†’âŸ¨ Î¾-Â·â‚‚ V-Æ› (Î²-Æ› V-zero) âŸ©
    (Æ› "z" â‡’ sucá¶œ Â· (sucá¶œ Â· ` "z")) Â· (sucá¶œ Â· (sucá¶œ Â· `zero))
  â€”â†’âŸ¨ Î¾-Â·â‚‚ V-Æ› (Î¾-Â·â‚‚ V-Æ› (Î²-Æ› V-zero)) âŸ©
    (Æ› "z" â‡’ sucá¶œ Â· (sucá¶œ Â· ` "z")) Â· (sucá¶œ Â· (`suc `zero))
  â€”â†’âŸ¨ Î¾-Â·â‚‚ V-Æ› (Î²-Æ› (V-suc V-zero)) âŸ©
    (Æ› "z" â‡’ sucá¶œ Â· (sucá¶œ Â· ` "z")) Â· (`suc `suc `zero)
  â€”â†’âŸ¨ Î²-Æ› (V-suc (V-suc V-zero)) âŸ©
    sucá¶œ Â· (sucá¶œ Â· `suc `suc `zero)
  â€”â†’âŸ¨ Î¾-Â·â‚‚ V-Æ› (Î²-Æ› (V-suc (V-suc V-zero))) âŸ©
    sucá¶œ Â· (`suc `suc `suc `zero)
  â€”â†’âŸ¨ Î²-Æ› (V-suc (V-suc (V-suc V-zero))) âŸ©
   `suc (`suc (`suc (`suc `zero)))
  âˆŽ
\end{code}

In the next chapter, we will see how to compute such reduction sequences.


#### Exercise (`mul-ex`)

Using the term `mul` you defined earlier, write out the reduction
sequence demonstrating that two times two is four.


## Syntax of types

We have just two types:

  * Functions, `A â‡’ B`
  * Naturals, `` `â„• ``

As before, to avoid overlap we use variants of the names used by Agda.

Here is the syntax of types in BNF.

    A, B, C  ::=  A â‡’ B | `â„•

And here it is formalised in Agda.

\begin{code}
infixr 6 _â‡’_

data Type : Set where
  _â‡’_ : Type â†’ Type â†’ Type
  `â„• : Type
\end{code}

### Precedence

As in Agda, functions of two or more arguments are represented via
currying. This is made more convenient by declaring `_â‡’_` to
associate to the right and `_Â·_` to associate to the left.
Thus,

* ``(`â„• â‡’ `â„•) â‡’ `â„• â‡’ `â„•`` stands for ``((`â„• â‡’ `â„•) â‡’ (`â„• â‡’ `â„•))``
* `plus Â· two Â· two` stands for `(plus Â· two) Â· two`.

### Quiz

* What is the type of the following term?

    Æ› "s" â‡’ ` "s" Â· (` "s"  Â· `zero)

  1. `` (`â„• â‡’ `â„•) â‡’ (`â„• â‡’ `â„•) ``
  2. `` (`â„• â‡’ `â„•) â‡’ `â„• ``
  3. `` `â„• â‡’ (`â„• â‡’ `â„•) ``
  4. `` `â„• â‡’ `â„• â‡’ `â„• ``
  5. `` `â„• â‡’ `â„• ``
  6. `` `â„• ``

  Give more than one answer if appropriate.

* What is the type of the following term?

    (Æ› "s" â‡’ ` "s" Â· (` "s"  Â· `zero)) Â· sucáµ

  1. `` (`â„• â‡’ `â„•) â‡’ (`â„• â‡’ `â„•) ``
  2. `` (`â„• â‡’ `â„•) â‡’ `â„• ``
  3. `` `â„• â‡’ (`â„• â‡’ `â„•) ``
  4. `` `â„• â‡’ `â„• â‡’ `â„• ``
  5. `` `â„• â‡’ `â„• ``
  6. `` `â„• ``

  Give more than one answer if appropriate.


## Typing

### Contexts

While reduction considers only closed terms, typing must
consider terms with free variables.  To type a term,
we must first type its subterms, and in particular in the
body of an abstraction its bound variable may appear free.

A _context_ associates variables with types.  We let `Î“` and `Î”` range
over contexts.  We write `âˆ…` for the empty context, and `Î“ , x â¦‚ A`
for the context that extends `Î“` by mapping variable `x` to type `A`.
For example,

* `` âˆ… , "s" â¦‚ `â„• â‡’ `â„• , "z" â¦‚ `â„• ``

is the context that associates variable ` "s" ` with type `` `â„• â‡’ `â„• ``,
and variable ` "z" ` with type `` `â„• ``.

Contexts are formalised as follows.

\begin{code}
infixl 5  _,_â¦‚_

data Context : Set where
  âˆ…     : Context
  _,_â¦‚_ : Context â†’ Id â†’ Type â†’ Context
\end{code}

### Lookup judgment

We have two forms of _judgment_.  The first is written

    Î“ âˆ‹ x â¦‚ A

and indicates in context `Î“` that variable `x` has type `A`.
It is called _lookup_.
For example

* `` âˆ… , "s" â¦‚ `â„• â‡’ `â„• , "z" â¦‚ `â„• âˆ‹ "z" â¦‚ `â„• ``
* `` âˆ… , "s" â¦‚ `â„• â‡’ `â„• , "z" â¦‚ `â„• âˆ‹ "s" â¦‚ `â„• â‡’ `â„• ``

give us the types associated with variables ` "z" ` and ` "s" `, respectively.
The symbol `âˆ‹` (pronounced "ni", for "in" backwards) is chosen because
checking that `Î“ âˆ‹ x â¦‚ A` is analogous to checking whether `x â¦‚ A` appears
in a list corresponding to `Î“`.

If two variables in a context have the same name, then lookup
should return the most recently bound variable, which _shadows_
the other variables.  For example,

* `` âˆ… , "x" : `â„• â‡’ `â„• , "x" : `â„• âˆ‹ "x" â¦‚ `â„•

Here `` "x" â¦‚ `â„• â‡’ `â„• `` is shadowed by `` "x" â¦‚ `â„• ``.

Lookup is formalised as follows.
\begin{code}
infix  4  _âˆ‹_â¦‚_

data _âˆ‹_â¦‚_ : Context â†’ Id â†’ Type â†’ Set where

  Z : âˆ€ {Î“ x A}
      ------------------
    â†’ Î“ , x â¦‚ A âˆ‹ x â¦‚ A

  S : âˆ€ {Î“ x y A B}
    â†’ x â‰¢ y
    â†’ Î“ âˆ‹ x â¦‚ A
      ------------------
    â†’ Î“ , y â¦‚ B âˆ‹ x â¦‚ A
\end{code}

The constructors `Z` and `S` correspond roughly to the constructors
`here` and `there` for the element-of relation `_âˆˆ_` on lists.
Constructor `S` takes an additional parameter, which ensures that
when we look up a variable that it is not _shadowed_ by another
variable with the same name earlier in the list.

### Typing judgment

The second judgment is written

    Î“ âŠ¢ M â¦‚ A

and indicates in context `Î“` that term `M` has type `A`.
Context `Î“` provides types for all the free variables in `M`.
For example

* `` âˆ… , "s" â¦‚ `â„• â‡’ `â„• , "z" â¦‚ `â„• âŠ¢ ` "z" â¦‚ `â„•
* `` âˆ… , "s" â¦‚ `â„• â‡’ `â„• , "z" â¦‚ `â„• âŠ¢ ` "s" â¦‚ `â„• â‡’ `â„•
* `` âˆ… , "s" â¦‚ `â„• â‡’ `â„• , "z" â¦‚ `â„• âŠ¢ ` ` "s" Â· ` "z" â¦‚  `â„• ``
* `` âˆ… , "s" â¦‚ `â„• â‡’ `â„• , "z" â¦‚ `â„• âŠ¢ ` "s" Â· (` "s" Â· ` "z") â¦‚  `â„• ``
* `` âˆ… , "s" â¦‚ `â„• â‡’ `â„• âŠ¢ (Æ› "z" â‡’ ` "s" Â· (` "s" Â· ` "z")) â¦‚  `â„• â‡’ `â„• ``
* `` âˆ… âŠ¢ Æ› "s" â‡’ Æ› "z" â‡’ ` "s" Â· (` "s" Â· ` "z")) â¦‚  (`â„• â‡’ `â„•) â‡’ `â„• â‡’ `â„• ``

Typing is formalised as follows.
\begin{code}
infix  4  _âŠ¢_â¦‚_

data _âŠ¢_â¦‚_ : Context â†’ Term â†’ Type â†’ Set where

  -- Axiom
  âŠ¢` : âˆ€ {Î“ x A}
    â†’ Î“ âˆ‹ x â¦‚ A
       -------------
    â†’ Î“ âŠ¢ ` x â¦‚ A

  -- â‡’-I
  âŠ¢Æ› : âˆ€ {Î“ x N A B}
    â†’ Î“ , x â¦‚ A âŠ¢ N â¦‚ B
      -------------------
    â†’ Î“ âŠ¢ Æ› x â‡’ N â¦‚ A â‡’ B

  -- â‡’-E
  _Â·_ : âˆ€ {Î“ L M A B}
    â†’ Î“ âŠ¢ L â¦‚ A â‡’ B
    â†’ Î“ âŠ¢ M â¦‚ A
      -------------
    â†’ Î“ âŠ¢ L Â· M â¦‚ B

  -- â„•-Iâ‚
  âŠ¢zero : âˆ€ {Î“}
      --------------
    â†’ Î“ âŠ¢ `zero â¦‚ `â„•

  -- â„•-Iâ‚‚
  âŠ¢suc : âˆ€ {Î“ M}
    â†’ Î“ âŠ¢ M â¦‚ `â„•
      ---------------
    â†’ Î“ âŠ¢ `suc M â¦‚ `â„•

  -- â„•-E
  âŠ¢case : âˆ€ {Î“ L M x N A}
    â†’ Î“ âŠ¢ L â¦‚ `â„•
    â†’ Î“ âŠ¢ M â¦‚ A
    â†’ Î“ , x â¦‚ `â„• âŠ¢ N â¦‚ A
      -------------------------------------
    â†’ Î“ âŠ¢ `case L [zeroâ‡’ M |suc x â‡’ N ] â¦‚ A

  âŠ¢Î¼ : âˆ€ {Î“ x M A}
    â†’ Î“ , x â¦‚ A âŠ¢ M â¦‚ A
      -----------------
    â†’ Î“ âŠ¢ Î¼ x â‡’ M â¦‚ A
\end{code}

Each type rule is named after the constructor for the
corresponding term.

Most of the rules have a second name,
derived from a convention in logic, whereby the rule is
named after the type connective that it concerns;
rules to introduce and to
eliminate each connective are labeled `-I` and `-E`, respectively. As we
read the rules from top to bottom, introduction and elimination rules
do what they say on the tin: the first _introduces_ a formula for the
connective, which appears in the conclusion but not in the premises;
while the second _eliminates_ a formula for the connective, which appears in
a premise but not in the conclusion. An introduction rule describes
how to construct a value of the type (abstractions yield functions,
`` `suc `` and `` `zero `` yield naturals), while an elimination rule describes
how to deconstruct a value of the given type (applications use
functions, case expressions use naturals).

The rules are deterministic, in that at most one rule applies to every term.


### Checking inequality and postulating the impossible {#impossible}

The following function makes it convenient to assert an inequality.
\begin{code}
_â‰ _ : âˆ€ (x y : Id) â†’ x â‰¢ y
x â‰  y  with x â‰Ÿ y
...       | no  xâ‰¢y  =  xâ‰¢y
...       | yes _    =  âŠ¥-elim impossible
  where postulate impossible : âŠ¥
\end{code}
Here `_â‰Ÿ_` is the function that tests two identifiers for equality.
We intend to apply the function only when the
two arguments are indeed unequal, and indicate that the second
case should never arise by postulating a term `impossible` of
with the empty type `âŠ¥`.  If we use `C `N to normalise the term

    "a" â‰  "a"

Agda will return an answer warning us that the impossible has occurred:

    âŠ¥-elim (.plta.Lambda.impossible "a" "a" refl)

While postulating the impossible is a useful technique, it must be
used with care, since such postulation could allow us to provide
evidence of _any_ proposition whatsoever, regardless of its truth.


### Example type derivations {#derivation}

Type derivations correspond to trees. In informal notation, here
is a type derivation for the Church numberal two:

                               âˆ‹s                         âˆ‹z
                               -------------------- `_    --------------- `_
    âˆ‹s                         Î“â‚‚ âŠ¢ ` "s" â¦‚ `â„• â‡’ `â„•       Î“â‚‚ âŠ¢ ` "z" â¦‚ `â„•
    -------------------- `_    ------------------------------------------ _Â·_
    Î“â‚‚ âŠ¢ ` "s" â¦‚ `â„• â‡’ `â„•       Î“â‚‚ âŠ¢ ` "s" Â· ` "z" â¦‚ `â„•
    -------------------------------------------------- _Â·_
    Î“â‚‚ âŠ¢ ` "s" Â· (` "s" Â· ` "z") â¦‚ `â„•
    ---------------------------------------------- âŠ¢Æ›
    Î“â‚ âŠ¢ Æ› "z" â‡’ ` "s" Â· (` "s" Â· ` "z") â¦‚ `â„• â‡’ `â„•
    ----------------------------------------------------------------- âŠ¢Æ›
    âˆ… âŠ¢ Æ› "s" â‡’ Æ› "z" â‡’ ` "s" Â· (` "s" Â· ` "z") â¦‚ (`â„• â‡’ `â„•) â‡’ `â„• â‡’ `â„•

where `âˆ‹s` and `âˆ‹z` abbreviate the two derivations:

                 ---------------- Z
    "s" â‰¢ "z"    Î“â‚ âˆ‹ "s" â¦‚ `â„• â‡’ `â„•
    ----------------------------- S        ------------- Z
    Î“â‚‚ âˆ‹ "s" â¦‚ `â„• â‡’ `â„•                       Î“â‚‚ âˆ‹ "z" â¦‚ `â„•

and where ``Î“â‚ = âˆ… , s â¦‚ `â„• â‡’ `â„•`` and ``Î“â‚‚ = âˆ… , s â¦‚ `â„• â‡’ `â„• , z â¦‚ `â„•``.
In fact, `plusá¶œ` also has this typing derivation if we replace `âˆ…` by an
arbitrary context `Î“`, and `` `â„• `` by an arbitrary type `A`, but the above
will suffice for our purposes.

Here is the above typing derivation formalised in Agda.
\begin{code}
Ch : Type â†’ Type
Ch A = (A â‡’ A) â‡’ A â‡’ A

âŠ¢twoá¶œ : âˆ… âŠ¢ twoá¶œ â¦‚ Ch `â„•
âŠ¢twoá¶œ = âŠ¢Æ› (âŠ¢Æ› (âŠ¢` âˆ‹s Â· (âŠ¢` âˆ‹s Â· âŠ¢` âˆ‹z)))
  where
  âˆ‹s = S ("s" â‰  "z") Z
  âˆ‹z = Z
\end{code}

Here are the typings corresponding to computing two plus two.
\begin{code}
âŠ¢two : âˆ… âŠ¢ two â¦‚ `â„•
âŠ¢two = âŠ¢suc (âŠ¢suc âŠ¢zero)

âŠ¢plus : âˆ… âŠ¢ plus â¦‚ `â„• â‡’ `â„• â‡’ `â„•
âŠ¢plus = âŠ¢Î¼ (âŠ¢Æ› (âŠ¢Æ› (âŠ¢case (âŠ¢` âˆ‹m) (âŠ¢` âˆ‹n)
         (âŠ¢suc (âŠ¢` âˆ‹+ Â· âŠ¢` âˆ‹mâ€² Â· âŠ¢` âˆ‹nâ€²)))))
  where
  âˆ‹+  = (S ("+" â‰  "m") (S ("+" â‰  "n") (S ("+" â‰  "m") Z)))
  âˆ‹m  = (S ("m" â‰  "n") Z)
  âˆ‹n  = Z
  âˆ‹mâ€² = Z
  âˆ‹nâ€² = (S ("n" â‰  "m") Z)

âŠ¢2+2 : âˆ… âŠ¢ plus Â· two Â· two â¦‚ `â„•
âŠ¢2+2 = âŠ¢plus Â· âŠ¢two Â· âŠ¢two
\end{code}
Here the two lookup judgments `âˆ‹m` and `âˆ‹mâ€²` refer to two different
bindings of variables named `"m"`.  In contrast, the two judgments `âˆ‹n` and
`âˆ‹nâ€²` both refer to the same binding of `"n"` but accessed in different
contexts, the first where "n" is the last binding in the context, and
the second after "m" is bound in the successor branch of the case.

And here are typings for the remainder of the Church example.
\begin{code}
âŠ¢plusá¶œ : âˆ… âŠ¢ plusá¶œ â¦‚ Ch `â„• â‡’ Ch `â„• â‡’ Ch `â„•
âŠ¢plusá¶œ = âŠ¢Æ› (âŠ¢Æ› (âŠ¢Æ› (âŠ¢Æ› (âŠ¢` âˆ‹m Â· âŠ¢` âˆ‹s Â· (âŠ¢` âˆ‹n Â· âŠ¢` âˆ‹s Â· âŠ¢` âˆ‹z)))))
  where
  âˆ‹m = S ("m" â‰  "z") (S ("m" â‰  "s") (S ("m" â‰  "n") Z))
  âˆ‹n = S ("n" â‰  "z") (S ("n" â‰  "s") Z)
  âˆ‹s = S ("s" â‰  "z") Z
  âˆ‹z = Z

âŠ¢sucá¶œ : âˆ… âŠ¢ sucá¶œ â¦‚ `â„• â‡’ `â„•
âŠ¢sucá¶œ = âŠ¢Æ› (âŠ¢suc (âŠ¢` âˆ‹n))
  where
  âˆ‹n = Z

âŠ¢2á¶œ : âˆ… âŠ¢ twoá¶œ Â· sucá¶œ Â· `zero â¦‚ `â„•
âŠ¢2á¶œ = âŠ¢twoá¶œ Â· âŠ¢sucá¶œ Â· âŠ¢zero

âŠ¢2+2á¶œ : âˆ… âŠ¢ plusá¶œ Â· twoá¶œ Â· twoá¶œ Â· sucá¶œ Â· `zero â¦‚ `â„•
âŠ¢2+2á¶œ = âŠ¢plusá¶œ Â· âŠ¢twoá¶œ Â· âŠ¢twoá¶œ Â· âŠ¢sucá¶œ Â· âŠ¢zero
\end{code}

### Interaction with Agda

Construction of a type derivation may be done interactively.
Start with the declaration:

    âŠ¢sucá¶œ : âˆ… âŠ¢ sucá¶œ â¦‚ `â„• â‡’ `â„•
    âŠ¢sucá¶œ = ?

Typing C-l causes Agda to create a hole and tell us its expected type.

    âŠ¢sucá¶œ = { }0
    ?0 : âˆ… âŠ¢ sucá¶œ â¦‚ `â„• â‡’ `â„•

Now we fill in the hole by typing C-c C-r. Agda observes that
the outermost term in `sucá¶œ` in `âŠ¢Æ›`, which is typed using `Æ›`. The
`Æ›` rule in turn takes one argument, which Agda leaves as a hole.

    âŠ¢sucá¶œ = âŠ¢Æ› { }1
    ?1 : âˆ… , "n" â¦‚ `â„• âŠ¢ `suc ` "n" â¦‚ `â„•

We can fill in the hole by type C-c C-r again.

    âŠ¢sucá¶œ = âŠ¢Æ› (âŠ¢suc { }2)
    ?2 : âˆ… , "n" â¦‚ `â„• âŠ¢ ` "n" â¦‚ `â„•

And again.

    âŠ¢sucâ€² = âŠ¢Æ› (âŠ¢suc (âŠ¢` { }3))
    ?3 : âˆ… , "n" â¦‚ `â„• âˆ‹ "n" â¦‚ `â„•

A further attempt with C-c C-r yields the message:

    Don't know which constructor to introduce of Z or S

We can fill in `Z` by hand. If we type C-c C-space, Agda will confirm we are done.

    âŠ¢sucâ€² = âŠ¢Æ› (âŠ¢suc (âŠ¢` Z))

The entire process can be automated using Agsy, invoked with C-c C-a.

Chapter [Inference]({{ site.baseurl }}{% link out/plta/DeBruijn.md %})
will show how to use Agda to compute type derivations directly.


### Lookup is functional

The lookup relation `Î“ âˆ‹ x â¦‚ A` is functional, in that for each `Î“` and `x`
there is at most one `A` such that the judgment holds.
\begin{code}
âˆ‹-functional : âˆ€ {Î“ x A B} â†’ Î“ âˆ‹ x â¦‚ A â†’ Î“ âˆ‹ x â¦‚ B â†’ A â‰¡ B
âˆ‹-functional Z        Z          =  refl
âˆ‹-functional Z        (S xâ‰¢ _)   =  âŠ¥-elim (xâ‰¢ refl)
âˆ‹-functional (S xâ‰¢ _) Z          =  âŠ¥-elim (xâ‰¢ refl)
âˆ‹-functional (S _ âˆ‹x) (S _ âˆ‹xâ€²)  =  âˆ‹-functional âˆ‹x âˆ‹xâ€²
\end{code}

The typing relation `Î“ âŠ¢ M â¦‚ A` is not functional. For example, in any `Î“`
the term `Æ› "x" â‡’ "x"` has type `A â‡’ A` for any type `A`.

### Non-examples

We can also show that terms are _not_ typeable.  For example, here is
a formal proof that it is not possible to type the term
`` `zero Â· `suc `zero ``.  It cannot be typed, because doing so
requires that the first term in the application is both a natural and
a function.

\begin{code}
nopeâ‚ : âˆ€ {A} â†’ Â¬ (âˆ… âŠ¢ `zero Â· `suc `zero â¦‚ A)
nopeâ‚ (() Â· _)
\end{code}

As a second example, here is a formal proof that it is not possible to
type `` Æ› "x" â‡’ ` "x" Â· ` "x" `` It cannot be typed, because
doing so requires types `A` and `B` such that `A â‡’ B â‰¡ A`.

\begin{code}
nopeâ‚‚ : âˆ€ {A} â†’ Â¬ (âˆ… âŠ¢ Æ› "x" â‡’ ` "x" Â· ` "x" â¦‚ A)
nopeâ‚‚ (âŠ¢Æ› (âŠ¢` âˆ‹x Â· âŠ¢` âˆ‹xâ€²))  =  contradiction (âˆ‹-functional âˆ‹x âˆ‹xâ€²)
  where
  contradiction : âˆ€ {A B} â†’ Â¬ (A â‡’ B â‰¡ A)
  contradiction ()
\end{code}


#### Quiz

For each of the following, given a type `A` for which it is derivable,
or explain why there is no such `A`.

1. `` âˆ… , "y" â¦‚ `â„• â‡’ `â„• , "x" â¦‚ `â„• âŠ¢ ` "y" Â· ` "x" â¦‚ A ``
2. `` âˆ… , "y" â¦‚ `â„• â‡’ `â„• , "x" â¦‚ `â„• âŠ¢ ` "x" Â· ` "y" â¦‚ A ``
3. `` âˆ… , "y" â¦‚ `â„• â‡’ `â„• âŠ¢ Æ› "x" â‡’ ` "y" Â· ` "x" â¦‚ A ``

For each of the following, give type `A`, `B`, and `C` for which it is derivable,
or explain why there are no such types.

1. `` âˆ… , "x" â¦‚ A âŠ¢ ` "x" Â· ` "x" â¦‚ B ``
2. `` âˆ… , "x" â¦‚ A , "y" â¦‚ B âŠ¢ Æ› "z" â‡’ ` "x" Â· (` "y" Â· ` "z") â¦‚ C ``


#### Exercise (`mul-type`)

Using the term `mul` you defined earlier, write out the derivation
showing that it is well-typed.


## Unicode

This chapter uses the following unicode

    â‡’    U+21D2:  RIGHTWARDS DOUBLE ARROW (\=>)
    Æ›    U+019B:  LATIN SMALL LETTER LAMBDA WITH STROKE (\Gl-)
    Â·    U+00B7:  MIDDLE DOT (\cdot)
    ðŸ˜‡   U+1F607: SMILING FACE WITH HALO
    ðŸ˜ˆ   U+1F608: SMILING FACE WITH HORNS
    â€”    U+2014:  EM DASH (\em)
    â†     U+21A0:  RIGHTWARDS TWO HEADED ARROW (\rr-)
    Î¾    U+03BE:  GREEK SMALL LETTER XI (\Gx or \xi)
    Î²    U+03B2:  GREEK SMALL LETTER BETA (\Gb or \beta)
    âˆ‹    U+220B:  CONTAINS AS MEMBER (\ni)
    âŠ¢    U+22A2:  RIGHT TACK (\vdash or \|-)
    â¦‚    U+2982:  Z NOTATION TYPE COLON (\:)
